
include "samples/common_astra.testo"

network nat {
	mode: "nat"
}

machine astra {
	cpus: 2
	iso: "${ISO_DIR}/astralinux-orel.iso"
	ram: 4096Mb
	disk_size: 20Gb

	nic nat: {
		attached_to: "nat"
	}
}

param host_name "builder"
param login "user"
param password "12345678"

test astra_install_os {
	astra install_astra_gui("${host_name}", "${login}", "${password}")
}

test astra_install_guest_additions: astra_install_os {
	astra install_guest_additions_gui("${ISO_DIR}", "${password}")
}

test astra_install_extra_packages: astra_install_guest_additions {
		astra {
			exec bash """
				apt update
				apt install -y git libssl-dev
			"""
		}
}

test astra_install_cmake: astra_install_extra_packages {
	astra {
		exec bash """
			cd /root
			wget https://github.com/Kitware/CMake/archive/v3.16.5.tar.gz -O cmake.tar.gz
			tar xf cmake.tar.gz
			cd CMake-3.16.5
			./configure
			make
			make install
			cmake --version
		""" timeout 30m
	}
}

test astra_build_onnxruntime: astra_install_cmake {
	astra {
		copyto "3rd_party/onnxruntime" "/root/onnxruntime_patches"
		exec bash """
			apt install -y python3-dev
			cd /root
			git clone https://github.com/microsoft/onnxruntime.git
			cd onnxruntime
			git checkout v1.1.0
			git apply ../onnxruntime_patches/single_thread.patch
			git apply ../onnxruntime_patches/rename_dll.patch
			./build.sh --config Release --parallel --disable_contrib_ops --build_shared_lib --skip_tests
			mkdir /root/onnxruntime_release
			mkdir /root/onnxruntime_release/include
			mkdir /root/onnxruntime_release/lib
			cp build/Linux/Release/libonnxruntime_testo.so* /root/onnxruntime_release/lib
			cp include/onnxruntime/core/session/* /root/onnxruntime_release/include
		""" timeout 2h
	}
}

test astra_build_testo: astra_build_onnxruntime {
	astra {
		copyto "." "/root/testo"
		exec bash """
			apt install -y libvirt-dev rpm
			cd /root/
			mkdir testo_build
			cd testo_build
			cmake -DONNX_RUNTIME_DIR=/root/onnxruntime_release ../testo
			make testo-package testo-guest-additions-iso -j 2
		"""
		copyfrom "/root/testo_build/out/pkg" "${OUT_DIR}/astra"
	}
}
