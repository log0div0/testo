
include "common.testo"

machine centos {
	cpus: 4
	iso: "${ISO_DIR}/CentOS-8-x86_64-1905-dvd1.iso"
	ram: 4Gb
	disk main: {
		size: 32Gb
	}
	nic nat: {
		attached_to: "nat"
	}
}

test centos_install_os {
	centos {
		start
		wait "Install CentOS Linux 8"; press Enter
		wait "Press [Esc]"; press Esc
		wait "What language would you" timeout 10m; mouse click "Continue"
		mouse click "Installation Dest"
		mouse click "Done"
		sleep 2s
		mouse click "Software selection"
		mouse click "Minimal Install"
		mouse click "Done".from_top(0)
		sleep 5s
		mouse click js "find_text('Begin Installation').match_color(null, 'blue').from_top(0)"
		mouse click "Root Password".from_top(0);
		wait "Enter a password"; type "${password}"
		press Tab; type "${password}"
		mouse click "Done".from_top(0)
		mouse move 200 200
		mouse click "Done".from_top(0)
		wait "CentOS Linux is now successfully installed" timeout 20m
		unplug dvd
		mouse click "Reboot".from_bottom(0)
		wait "localhost login"
	}
}

[no_snapshots: true]
test centos_install_guest_additions: centos_install_os {
	centos {
		wait "localhost login:"; type "root"; press Enter
		wait "Password:"; type "${password}"; press Enter
		wait "root@localhost"

		plug dvd "${ISO_DIR}/testo-guest-additions.iso"; sleep 5s
		type "mount /dev/cdrom /media"; press Enter
		wait "mounted read-only"; type "rpm -i /media/testo-guest-additions.rpm && echo result is $?"; press Enter
		wait "result is 0"
		type "umount /media"; press Enter
		sleep 5s; unplug dvd

		exec bash "echo hello world"
	}
}

test centos_install_packages: centos_install_guest_additions {
	centos {
		plug dvd "${ISO_DIR}/CentOS-8-x86_64-1905-dvd1.iso"
		sleep 5s
		exec bash """
			mkdir /media/CentOS
			mount /dev/cdrom /media/CentOS
			yum --disablerepo=* --enablerepo=c8-media* -y install git gcc gcc-c++ make openssl-devel python3-devel libvirt-devel tar wget vim rpm-build libguestfs-devel
			cd /root
			nmcli connection up `ls /proc/sys/net/ipv4/conf/ | grep ens`
			wget https://github.com/Kitware/CMake/releases/download/v3.19.2/cmake-3.19.2-Linux-x86_64.sh -O cmake.sh
			chmod +x cmake.sh
			./cmake.sh --skip-license --prefix=/usr
			cmake --version
		""" timeout 30m
	}
}

[no_snapshots: true]
test centos_build_onnxruntime_cpu: centos_install_packages {
	centos {
		linux_copy_onnxruntime_srcs()
		exec bash """
			export PATH=$PATH:/usr/local/bin
			mkdir -p /root/onnxruntime_build
			cd /root/onnxruntime_build
			cmake ../onnxruntime/cmake \
				-DCMAKE_BUILD_TYPE=Release \
				-DCMAKE_INSTALL_PREFIX=/root/onnxruntime_dist \
				-DCMAKE_INSTALL_LIBDIR=lib \
				-Donnxruntime_DISABLE_CONTRIB_OPS=ON \
				-Donnxruntime_BUILD_SHARED_LIB=ON \
				-Donnxruntime_BUILD_UNIT_TESTS=OFF \
				-Donnxruntime_USE_CUDA=OFF
			make install -j$(nproc)
		""" timeout 2h
		copyfrom "/root/onnxruntime_dist" "${TMP_DIR}/onnxruntime_dist_centos_cpu"
	}
}

test centos_install_cuda: centos_install_packages {
	centos {
		plug flash nvidia
		sleep 5s
		exec bash """
			mount /dev/sdb1 /media
			ls /media
			/media/${cuda_linux_filename} --silent --toolkit --override
			/usr/local/cuda/bin/nvcc --version
			tar xf /media/${cudnn_linux_filename} -C /root
			ls /root
			umount /media
		""" timeout 10m
		sleep 5s
		unplug flash nvidia
	}
}

[no_snapshots: true]
test centos_build_onnxruntime_gpu: centos_install_cuda {
	centos {
		linux_copy_onnxruntime_srcs()
		exec bash """
			export PATH="$PATH:/usr/local/bin:/usr/local/cuda/bin"
			mkdir -p /root/onnxruntime_build
			cd /root/onnxruntime_build
			cmake ../onnxruntime/cmake \
				-DCMAKE_BUILD_TYPE=Release \
				-DCMAKE_INSTALL_PREFIX=/root/onnxruntime_dist \
				-DCMAKE_INSTALL_LIBDIR=lib \
				-Donnxruntime_DISABLE_CONTRIB_OPS=ON \
				-Donnxruntime_BUILD_SHARED_LIB=ON \
				-Donnxruntime_BUILD_UNIT_TESTS=OFF \
				-Donnxruntime_USE_CUDA=ON \
				-Donnxruntime_CUDNN_HOME=/root/cuda
			make install -j$(nproc)
		""" timeout 2h
		copyfrom "/root/onnxruntime_dist" "${TMP_DIR}/onnxruntime_dist_centos_gpu"
	}
}

[no_snapshots: true]
test centos_build_testo_cpu: centos_install_packages {
	centos {
		copyto "${TMP_DIR}/onnxruntime_dist_centos_cpu" "/root/onnxruntime_dist"
		copyto "${TESTO_SRC_DIR}/3rd_party" "/root/testo/3rd_party"
		copyto "${TESTO_SRC_DIR}/docs" "/root/testo/docs"
		copyto "${TESTO_SRC_DIR}/lib" "/root/testo/lib"
		copyto "${TESTO_SRC_DIR}/pkg" "/root/testo/pkg"
		copyto "${TESTO_SRC_DIR}/src" "/root/testo/src"
		copyto "${TESTO_SRC_DIR}/CMakeLists.txt" "/root/testo/CMakeLists.txt"
		exec bash """
			export PATH=$PATH:/usr/local/bin
			cd /root/
			mkdir testo_build
			cd testo_build
			cmake ../testo \
				-DCMAKE_BUILD_TYPE=Release \
				-DCPACK_GENERATOR=RPM \
				-DONNX_RUNTIME_DIR=/root/onnxruntime_dist \
				-DUSE_CUDA=off
			make testo-package -j$(nproc)
			ls out/pkg
		""" timeout 30m
		copyfrom "/root/testo_build/out/pkg/testo.rpm" "${OUT_DIR}/testo-cpu.rpm"
	}
}

[no_snapshots: true]
test centos_build_testo_gpu: centos_install_cuda {
	centos {
		copyto "${TMP_DIR}/onnxruntime_dist_centos_gpu" "/root/onnxruntime_dist"
		copyto "${TESTO_SRC_DIR}/3rd_party" "/root/testo/3rd_party"
		copyto "${TESTO_SRC_DIR}/docs" "/root/testo/docs"
		copyto "${TESTO_SRC_DIR}/lib" "/root/testo/lib"
		copyto "${TESTO_SRC_DIR}/pkg" "/root/testo/pkg"
		copyto "${TESTO_SRC_DIR}/src" "/root/testo/src"
		copyto "${TESTO_SRC_DIR}/CMakeLists.txt" "/root/testo/CMakeLists.txt"
		exec bash """
			export PATH="$PATH:/usr/local/bin:/usr/local/cuda/bin"
			cd /root/
			mkdir testo_build
			cd testo_build
			cmake ../testo \
				-DCMAKE_BUILD_TYPE=Release \
				-DCPACK_GENERATOR=RPM \
				-DONNX_RUNTIME_DIR=/root/onnxruntime_dist \
				-DUSE_CUDA=on
			make testo-package -j$(nproc)
			ls out/pkg
		""" timeout 30m
		copyfrom "/root/testo_build/out/pkg/testo.rpm" "${OUT_DIR}/testo-gpu.rpm"
	}
}
