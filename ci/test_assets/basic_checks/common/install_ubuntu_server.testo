
include "definitions.testo"

macro install_ubuntu_server(host_name, login, password) {
	start
	sleep 2s
	wait "Language";
	sleep 2s;
	press Enter;
	wait "Install Ubuntu Server"; press Enter;
	wait "Choose the language";	press Enter
	wait "Select your location"; press Enter
	wait "Detect keyboard layout?";	press Enter
	wait "Country of origin for the keyboard"; press Enter
	wait "Keyboard layout"; press Enter
	wait "No network interfaces detected" || "Primary network interface" || "Hostname:" timeout 5m
	if (check "No network interfaces detected") {
		press Right, Enter
	} else if (check "Primary network interface"){
		press Enter
	}
	wait "Hostname:" timeout 30s; press Backspace*36; type "${host_name}"; press Enter
	wait "Full name for the new user"; type "${login}"; press Enter
	wait "Username for your account"; press Enter
	wait "Choose a password for the new user"; type "${password}"; press Enter
	wait "Re-enter password to verify"; type "${password}"; press Enter
	if (check "Use weak password?" timeout 3s) {
		press Left, Enter
	}
	wait "Encrypt your home directory?"; press Enter
	wait "Select your time zone" || "your time zone is" timeout 2m; press Enter
	wait "Partitioning method"; press Enter
	wait "Select disk to partition"; press Enter
	wait "Write the changes to disks and configure LVM?"; press Left, Enter
	wait "Amount of volume group to use for guided partitioning"; press Enter
	wait "Write the changes to disks?"; press Left, Enter
	wait "HTTP proxy information" timeout 7m;
	if (DEFINED HTTP_PROXY) {
		type "${HTTP_PROXY}"
	}
	press Enter
	wait "How do you want to manage upgrades" timeout 6m; press Enter
	wait "Choose software to install"; press Enter
	wait "Install the GRUB boot loader to the master boot record?" timeout 10m; press Enter
	if (check "Device for boot loader installation" timeout 3s) {
		press Down, Enter
	}
	wait "Installation complete" timeout 10m; unplug dvd; press Enter
	wait "tty1"

	wait "ubuntu login:" timeout 2m;
	sleep 5s;

	for (i IN RANGE 10) {
		type "${login}"; press Enter
		wait "Password:"; sleep 5s; type "${password}"; press Enter

		if (check "Login incorrect" timeout 10s) {
			press LeftCtrl+C
			sleep 5s
		} else {
			break;
		}
	}

	wait "Welcome to Ubuntu"
}

test install_ubuntu_server {
	my_ubuntu_server install_ubuntu_server("ubuntu", "user", "1111")
}

macro enter_sudo(host_name, login, password) {
	type "sudo su"; press Enter;
	wait "[sudo] password for ${login}"; type "${password}"; press Enter
	wait "root@${host_name}"
}

macro install_guest_additions_ubuntu_server(host_name, login, password) {
	enter_sudo("${host_name}", "${login}", "${password}")
	plug dvd "/opt/testo-guest-additions.iso"; sleep 5s
	type "mount /dev/cdrom /media"; press Enter
	wait "mounting read-only"; type "dpkg -i /media/*.deb"; press Enter;
	wait "Setting up testo-guest-additions"
	type "umount /media"; press Enter;
	sleep 2s
	unplug dvd
}

test install_ubuntu_server_guest_additions: install_ubuntu_server {
	my_ubuntu_server {
		sleep 20s
		press Enter*20
		install_guest_additions_ubuntu_server("ubuntu", "user", "1111")
	}
}
