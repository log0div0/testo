
include "common.testo"

machine centos {
	cpus: 4
	iso: "${ISO_DIR}/CentOS-8-x86_64-1905-dvd1.iso"
	ram: 8Gb
	disk main: {
		size: 32Gb
	}
	nic nat: {
		attached_to: "nat"
	}

	video main: {
		qemu_mode: "virtio"
	}
}

test centos_install_os {
	centos {
		start
		wait "Install CentOS Linux 8"; press Enter
		wait "Press [Esc]"; press Esc
		wait "What language would you" timeout 10m; mouse click "Continue"
		mouse click "Installation Dest"
		mouse click "Custom"
		mouse click "Done"

		mouse click "AVAILABLE".center_top().move_up(30)
		wait "ADD A NEW MOUNT POINT"
		mouse click "Mount Point:".from_bottom(0).right_center().move_right(220)
		mouse click "/boot"
		mouse click "Desired Capacity:".from_bottom(0).right_center().move_right(50)
		type "1Gb"
		mouse click "Add mount point"
		wait !"ADD A NEW MOUNT POINT"

		mouse click "AVAILABLE".center_top().move_up(30)
		wait "ADD A NEW MOUNT POINT"
		mouse click "Mount Point:".from_bottom(0).right_center().move_right(220)
		mouse click "swap"
		mouse click "Desired Capacity:".from_bottom(0).right_center().move_right(50)
		type "2Gb"
		mouse click "Add mount point"
		wait !"ADD A NEW MOUNT POINT"

		mouse click "AVAILABLE".center_top().move_up(30)
		wait "ADD A NEW MOUNT POINT"
		mouse click "Mount Point:".from_bottom(0).right_center().move_right(220)
		mouse click "/home".center().move_up(30)
		mouse click "Add mount point"
		wait !"ADD A NEW MOUNT POINT"

		mouse click "Done"
		mouse click "Accept Changes"

		sleep 2s
		mouse click "Software selection"
		mouse click "Workstation".from_top(0)
		mouse click "Done".from_top(0)
		sleep 5s
		mouse click js "find_text('Begin Installation').match_color(null, 'blue').from_top(0)"
		sleep 2s
		mouse click "Root Password".from_top(0);
		wait "Enter a password"; type "${password}"
		press Tab; type "${password}"
		mouse click "Done".from_top(0)
		sleep 2s
		mouse move 200 200
		mouse click "Done".from_top(0)
		wait "CentOS Linux is now successfully installed" timeout 20m
		unplug dvd
		mouse click "Reboot".from_bottom(0)
		wait "License" timeout 7m; mouse move 0 0
		mouse click "License Information"
		mouse click "I accept"
		mouse click "Done".from_top(0)
		sleep 2s
		mouse click "FINISH CONFIGURATION"
		wait "Welcome" timeout 2m; mouse click "Next".center_bottom();
		wait "Privacy"; mouse click "Next".center_bottom();
		wait "Online Accounts"; mouse click "Skip";
		wait "About You"; mouse click "Full Name".right_center().move_right(30);
		type "${login}"
		mouse click js "find_text('Next').match_color(null, 'blue').from_top(0)"
		wait "Set a Password";
		type "${password}"; press Tab; type "${password}";
		mouse move 0 0
		mouse click js "find_text('Next').match_color(null, 'blue').from_top(0)"
		mouse click "Start Using CentOS Linux"
		if (check "Not listed?" timeout 5s) {
			mouse click "${login}"
			wait "Password:"
			sleep 1s
			type "${password}"; press Enter
		}
		wait "GNOME Help" timeout 2m
		mouse click "Help".from_top(0); mouse click "Quit"
		wait !"GNOME Help"
	}
}

macro open_terminal() {
	mouse click "Activities"
	sleep 5s
	type "Term"; mouse click "Terminal".from_top(0)
	wait "${login}@localhost"
}

macro enter_sudo() {
	type "sudo su"; press Enter
	wait "[sudo] password for ${login}"; type "${password}"; press Enter
	wait "root@localhost"
}

macro open_virt_manager() {
	mouse click "Activities"
	sleep 5s
	type "virt"; mouse click "Virtual Machine"
	wait "Authentication Required"; type "${password}"; press Enter
	wait !"Authentication Required" && "Virtual Machine Manager"
}

macro exec_bash(script) {
	type "clear"; press Enter
	type "${script} && echo result is $?"; press Enter
	wait "result is 0"
}

[no_snapshots: true]
test centos_install_guest_additions: centos_install_os {
	centos {
		plug dvd "${ISO_DIR}/testo-guest-additions.iso"; sleep 5s
		open_terminal()
		enter_sudo()
		type "mount /dev/cdrom /media"; press Enter
		wait "mounted read-only"; type "rpm -i /media/testo-guest-additions.rpm && echo result is $?"; press Enter
		wait "result is 0"
		type "umount /media"; press Enter
		sleep 5s; unplug dvd

		exec bash "echo hello world"
		type "exit"; press Enter; type "exit"; press Enter; wait !"root@localhost"
	}
}

[no_snapshots: true]
test centos_prepare_1: centos_install_guest_additions {
	centos {
		plug dvd "${ISO_DIR}/CentOS-8-x86_64-1905-dvd1.iso"
		sleep 5s
		exec bash """
			mkdir /media/CentOS
			mount /dev/cdrom /media/CentOS
			yum --disablerepo=* --enablerepo=c8-media* -y install qemu-kvm libvirt iptables-ebtables dnsmasq virt-manager libguestfs edk2-ovmf
			nmcli connection up `ls /proc/sys/net/ipv4/conf/ | grep ens`
			yum -y install epel-release
			yum -y install ntfsprogs
		""" timeout 30m

		mouse click "Activities"
		sleep 5s
		type "Sett"; mouse click "Settings".from_top(0)
		wait "Bluetooth"
		mouse click "Wi-Fi".from_left(0); sleep 1s; press Down*11, Enter
		wait "Power Saving"; mouse click "5 minutes"; sleep 1s; mouse click "15 minutes"; sleep 1s; mouse click "15 minutes";  mouse click "Never"
		mouse click "Power".from_left(0); sleep 1s; press Down*2, Enter
		wait "Displays"; mouse click "Resolution";
		for (i IN RANGE "0" "100") {
			if (check "1024 x 768") {
				mouse click "1024 x 768"
				break
			}
			press Down*5
		} else {
			abort "fail"
		}

		if (check "Apply" timeout 3s) {
			mouse click "Apply".from_right(0); mouse click "Keep Changes"; sleep 2s;
		}
		press LeftAlt + F4;
		wait !"Displays"
	}
}


test centos_prepare_2: centos_prepare_1 {
	centos {
		copyto "${ISO_DIR}/ubuntu-16.04.6-server-amd64.iso" "/opt/ubuntu-16.04.6-server-amd64.iso"
		open_virt_manager()
	}
}

test centos_install_testo_cpu: centos_prepare_2 {
	centos {
		copyto "${OUT_DIR}/testo-cpu.rpm" "/home/${login}/testo-cpu.rpm"
		exec bash """
			rpm -i /home/${login}/testo-cpu.rpm
			testo version
		"""
	}
}

[no_snapshots: true]
test centos_install_testo_gpu: centos_prepare_2 {
	centos {
		copyto "${OUT_DIR}/testo-gpu.rpm" "/home/${login}/testo-gpu.rpm"
		exec bash """
			rpm -i /home/${login}/testo-gpu.rpm
			testo version
		"""
	}
}
