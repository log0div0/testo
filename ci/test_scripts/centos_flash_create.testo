include "centos.testo"

[no_snapshots: true]
test centos_flash_create: centos_install_testo_cpu {
	centos {
		copyto "${TEST_ASSETS_DIR}/flash_creation/flash_create.testo" "/opt/scripts/flash_create.testo"
		exec bash """
			set +o pipefail
			testo run /opt/scripts/flash_create.testo --test_spec flash_unexisting_folder 2>&1 > /dev/null | grep "Target folder /some/unexisting_folder doesn't exist"
			testo run /opt/scripts/flash_create.testo --test_spec flash_not_a_folder 2>&1 > /dev/null | grep "Specified folder /opt/ubuntu-16.04.6-server-amd64.iso is not a folder"
		""" timeout 30m
	}
}

test centos_backend_qemu_create_simple_flash: centos_install_testo_cpu {
	centos {
		copyto "${TEST_ASSETS_DIR}/flash_creation/flash_create.testo" "/opt/scripts/flash_create.testo"
		exec bash """
			testo run /opt/scripts/flash_create.testo --test_spec create_basic_flash
		""" timeout 30m
	}
}

[no_snapshots: true]
test centos_backend_qemu_meta_version0_flash_recovery: centos_backend_qemu_create_simple_flash {
	centos {
		exec python3 """
			import json

			with open('/var/lib/libvirt/testo/fd_metadata/basic_flash/basic_flash__init') as data_file:
			    data = json.load(data_file)

			del data['opaque']

			with open('/var/lib/libvirt/testo/fd_metadata/basic_flash/basic_flash__init', 'w') as data_file:
			    data = json.dump(data, data_file)
		"""

		exec bash """
			testo run /opt/scripts/flash_create.testo --test_spec create_basic_flash | grep "Creating flash drive basic_flash"
		""" timeout 30m

	}
}

[no_snapshots: true]
test centos_backend_qemu_meta_version1_flash_recovery: centos_backend_qemu_create_simple_flash {
	centos {
		exec python3 """
			import json

			with open('/var/lib/libvirt/testo/fd_metadata/basic_flash/basic_flash__init') as data_file:
			    data = json.load(data_file)

			del data['metadata_version']

			with open('/var/lib/libvirt/testo/fd_metadata/basic_flash/basic_flash__init', 'w') as data_file:
			    data = json.dump(data, data_file)
		"""

		exec bash """
			testo run /opt/scripts/flash_create.testo --test_spec create_basic_flash | grep "Creating flash drive basic_flash"
		""" timeout 30m

	}
}
