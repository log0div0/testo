
include "common.testo"

machine win10 {
	cpus: 4
	ram: 4Gb
	iso: "${ISO_DIR}/win10.iso"
	disk main: {
		size: 32Gb
	}
	nic nat: {
		attached_to: "nat"
		adapter_type: "e1000"
	}
}

[no_snapshots: true]
test win10_install_os_part_1 {
	win10 {
		start
		mouse click "Мой язык - русский" timeout 20m
		mouse click "Далее".from_bottom(0)
		mouse click "Установить"
		wait "Выберите операционную систему"
		mouse click "Windows 10 Корпоративная".from_top(0)
		mouse click "Далее".from_bottom(0)
		mouse click "Я принимаю условия лицензии"
		mouse click "Далее".from_bottom(0)
		wait "Выберите тип установки"
		mouse click "Выборочная"
		wait "Где вы хотите установить Windows?"
		mouse click "Далее".from_bottom(0)
		wait "Продолжить на выбранном языке?" timeout 1h interval 10s
	}
}

test win10_install_os_part_2: win10_install_os_part_1 {
	win10 {
		wait "Продолжить на выбранном языке?"
		mouse move 0 0; sleep 5s
		mouse click "Да".from_bottom(0)

		wait "Давайте начнем с региона. Это правильно?"
		mouse move 0 0; sleep 5s
		mouse click "Да".from_bottom(0)

		wait "Это правильная раскладка клавиатуры?"
		mouse move 0 0; sleep 5s
		mouse click "Да".from_bottom(0)

		wait "Вы хотите добавить вторую раскладку"
		mouse move 0 0; sleep 5s
		mouse click "Пропустить".from_bottom(0)

		wait "Лицензионное соглашение" timeout 20m
		mouse move 0 0; sleep 5s
		mouse click "Принять".from_bottom(0)

		wait "Какую учетную запись использовать?"
		mouse move 0 0; sleep 5s
		mouse click "Вместо этого присоединить к домену"

		wait "Какое имя вы хотите использовать?"
		mouse move 0 0; sleep 5s
		mouse click "Имя".from_bottom(0)
		press LeftAlt+LeftShift; sleep 5s; type "user"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Пароль".from_bottom(0)
		type "1111"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Подтверждение пароля".from_bottom(0)
		type "1111"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Контрольный вопрос (1 из 3)"
		mouse move 0 0; sleep 5s
		mouse move "Кличка первого домашнего животного"; sleep 5s; mouse click; sleep 5s
		press Tab; type "----"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Контрольный вопрос (2 из 3)"
		mouse move 0 0; sleep 5s
		mouse move "В каком городе вы родились"; sleep 5s; mouse click; sleep 5s
		press Tab; type "----"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Контрольный вопрос (3 из 3)"
		mouse move 0 0; sleep 5s
		mouse move "Ваше прозвище в детстве"; sleep 5s; mouse click; sleep 5s
		press Tab; type "----"; press Enter

		wait "Удобная работа на разных устройствах"
		mouse move 0 0; sleep 5s
		mouse click "Нет".from_bottom(0)

		wait "Выберите параметры конфиденциальности"
		mouse move 0 0; sleep 5s
		mouse click "Принять".from_bottom(0)

		wait "Корзина" timeout 20m
		unplug dvd
		sleep 10s
	}
}

[no_snapshots: true]
test win10_activate: win10_install_os_part_2 {
	win10 {
		mouse move "Activator"
		sleep 5s
		mouse click
		sleep 5s
		mouse dclick
		wait "Разрешить этому приложению"
		mouse click "Да".from_bottom(0)
		mouse click "Активировать Windows 10"
		wait "Постоянная активация прошла успешно"
		press LeftAlt+F4
	}
}

[no_snapshots: true]
test win10_enable_autologin: win10_activate {
	win10 {
		press LeftMeta+R
		wait "Введите имя программы"
		press LeftAlt+LeftShift; sleep 2s
		type "netplwiz"; press Enter
		mouse click "Учетные записи пользователей"
		mouse click "Требовать ввод имени пользователя и пароля"
		mouse click "Применить"
		wait "Автоматический вход в систему"
		type "1111"; press Tab
		type "1111"; press Enter
		wait !"Автоматический вход в систему"
		press LeftAlt+F4
		wait !"Учетные записи пользователей"
		shutdown
		start
		wait "Корзина"
	}
}

[no_snapshots: true]
test win10_install_hyperv: win10_enable_autologin {
	win10 {
		mouse click "поиск"; sleep 2s
		press LeftAlt+LeftShift; sleep 2s
		type "PowerShell"; sleep 2s
		mouse click "Запуск от имени администратора"
		wait "Разрешить этому приложению"
		mouse click "Да".from_bottom(0)
		wait "Администратор: Windows PowerShell"
		type "Enable-WindowsOptionalFeature -Online -FeatureName Microsoft-Hyper-V -All"
		press Enter
		wait "Вы хотите перезапустить компьютер сейчас"
		press Enter
		wait "Работа с обновлениями"
		wait "Корзина" timeout 10m
	}
}

[no_snapshots: true]
test win10_install_guest_additions: win10_install_hyperv {
	win10 {
		plug dvd "${ISO_DIR}/testo-guest-additions.iso"
		sleep 5s
		mouse click "CD-дисковод"
		sleep 5s
		mouse click "Выполнить autorun"
		wait "Testo Guest Additions"; press Enter
		mouse click "I accept the terms";
		mouse click "Next"
		wait "Destination Folder"; press Enter
		wait "Ready to install"; press Enter
		wait "Разрешить этому приложению"
		mouse click "Да".from_bottom(0)
		wait "Установить программное обеспечение для данного устройства?";
		mouse click "Установить".from_top(1)
		wait "Click the Finish button"; press Enter
		wait !"Click the Finish button"
		exec cmd "echo hello world"
		unplug dvd
		sleep 5s
	}
}

[no_snapshots: true]
test win10_install_python: win10_install_guest_additions {
	win10 {
		exec cmd """
			curl https://www.python.org/ftp/python/3.8.7/python-3.8.7-amd64.exe -o C:\\Users\\user\\Downloads\\python.exe
			C:\\Users\\user\\Downloads\\python.exe /quiet InstallAllUsers=1 PrependPath=1
		"""
		exec cmd """
			pip install pytest
		"""
	}
}

test win10_prepare: win10_install_python {
	win10 {
		copyto "${ISO_DIR}/ubuntu-16.04.6-server-amd64.iso" "C:\\ubuntu-16.04.6-server-amd64.iso"
		shutdown
	}
}
