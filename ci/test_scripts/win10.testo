
include "common.testo"

machine win10 {
	cpus: 4
	ram: 4Gb
	iso: "${ISO_DIR}/win10.iso"
	disk main: {
		size: 32Gb
	}
	nic nat: {
		attached_to: "nat"
		adapter_type: "e1000"
	}
}

[no_snapshots: true]
test win10_install_os_part_1 {
	win10 {
		start
		mouse click "Мой язык - русский" timeout 20m
		mouse click "Далее".from_bottom(0)
		mouse click "Установить"
		wait "Выберите операционную систему"
		mouse click "Windows 10 Корпоративная".from_top(0)
		mouse click "Далее".from_bottom(0)
		mouse click "Я принимаю условия лицензии"
		mouse click "Далее".from_bottom(0)
		wait "Выберите тип установки"
		mouse click "Выборочная"
		wait "Где вы хотите установить Windows?"
		mouse click "Далее".from_bottom(0)
		wait "Продолжить на выбранном языке?" timeout 1h interval 10s
	}
}

test win10_install_os_part_2: win10_install_os_part_1 {
	win10 {
		wait "Продолжить на выбранном языке?"
		mouse move 0 0; sleep 5s
		mouse click "Да".from_bottom(0)

		wait "Давайте начнем с региона. Это правильно?"
		mouse move 0 0; sleep 5s
		mouse click "Да".from_bottom(0)

		wait "Это правильная раскладка клавиатуры?"
		mouse move 0 0; sleep 5s
		mouse click "Да".from_bottom(0)

		wait "Вы хотите добавить вторую раскладку"
		mouse move 0 0; sleep 5s
		mouse click "Пропустить".from_bottom(0)

		wait "Лицензионное соглашение" timeout 20m
		mouse move 0 0; sleep 5s
		mouse click "Принять".from_bottom(0)

		wait "Какую учетную запись использовать?"
		mouse move 0 0; sleep 5s
		mouse click "Вместо этого присоединить к домену"

		wait "Какое имя вы хотите использовать?"
		mouse move 0 0; sleep 5s
		mouse click "Имя".from_bottom(0)
		mouse move 0 0; sleep 5s
		type "user" autoswitch LeftAlt+LeftShift; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Пароль".from_bottom(0)
		type "1111"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Подтверждение пароля".from_bottom(0)
		type "1111"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Контрольный вопрос (1 из 3)"
		mouse move 0 0; sleep 5s
		mouse move "Кличка первого домашнего животного"; sleep 5s; mouse click; sleep 5s
		press Tab; type "----"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Контрольный вопрос (2 из 3)"
		mouse move 0 0; sleep 5s
		mouse move "В каком городе вы родились"; sleep 5s; mouse click; sleep 5s
		press Tab; type "----"; press Enter

		mouse move 0 0; sleep 5s
		mouse click "Контрольный вопрос (3 из 3)"
		mouse move 0 0; sleep 5s
		mouse move "Ваше прозвище в детстве"; sleep 5s; mouse click; sleep 5s
		press Tab; type "----"; press Enter

		wait "Удобная работа на разных устройствах"
		mouse move 0 0; sleep 5s
		mouse click "Нет".from_bottom(0)

		wait "Выберите параметры конфиденциальности"
		mouse move 0 0; sleep 5s
		mouse click "Принять".from_bottom(0)

		wait "Корзина" timeout 20m
		unplug dvd
		sleep 10s
	}
}

[snapshots: "auto"]
test win10_test_ga_install: win10_install_os_part_2 {
	win10 {
		plug dvd "${OUT_DIR}/testo-guest-additions-${TESTO_HYPERVISOR}.iso"
		mouse click "CD-дисковод"
		sleep 5s
		mouse click "Выполнить autorun"
		wait "Testo Guest Additions"; press Enter
		mouse click "I accept the terms";
		mouse click "Next"
		wait "Destination Folder"; press Enter
		wait "Ready to install"; press Enter
		wait "Разрешить этому приложению"
		mouse click "Да".from_bottom(0)
		wait "Установить программное обеспечение для данного устройства?";
		mouse click "Установить".from_top(1)
		wait "Click the Finish button"; press Enter
		wait !"Click the Finish button"
		exec cmd "echo hello world"
		unplug dvd
		sleep 5s
	}
}

[no_snapshots: true]
test win10_test_ga_stop_service: win10_test_ga_install {
	win10 {
		press LeftMeta+R
		wait "Выполнить"
		type "services.msc" autoswitch LeftAlt+LeftShift; press Enter
		wait "Службы (локальные)"
		mouse click "DNS-клиент"
		press Down*20
		mouse dclick "Testo Guest Additions"
		wait "Свойства: Testo Guest Additions"
		exec cmd """type "C:\\Program Files\\Testo Lang\\Testo Guest Additions\\testo-guest-additions.txt""""
		mouse click "Остановить".from_left(1)
		wait "Остановлена"
	}
}

[snapshots: "auto"]
test win10_test_testo_install: win10_test_ga_install {
	win10 {
		copyto "${OUT_DIR}/testo.msi" "C:\\Users\\user\\Desktop\\testo.msi"
		mouse dclick "testo"
		wait "Welcome to the Testo Setup"; press Enter
		mouse click "I accept the terms";
		mouse click "Next"
		wait "Destination Folder"; press Enter
		wait "Ready to install"; press Enter
		wait "Разрешить этому приложению"
		mouse click "Да".from_bottom(0)
		wait "Click the Finish button"
		sleep 2s
		mouse click "Click the Finish button"
		press Enter
		wait !"Click the Finish button"

		exec cmd "testo version"
	}
}
