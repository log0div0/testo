
include "win10.testo"

test win10_test_ga_install: win10_install_os_part_2 {
	win10 {
		plug dvd "${OUT_DIR}/testo-guest-additions-${TESTO_HYPERVISOR}.iso"
		mouse click "CD-дисковод"
		sleep 5s
		mouse click "Выполнить autorun"
		wait "Testo Guest Additions"; press Enter
		mouse click "I accept the terms";
		mouse click "Next"
		wait "Destination Folder"; press Enter
		wait "Ready to install"; press Enter
		wait "Разрешить этому приложению"
		mouse click "Да".from_bottom(0)
		wait "Установить программное обеспечение для данного устройства?";
		mouse click "Установить".from_top(1)
		wait "Click the Finish button"; press Enter
		wait !"Click the Finish button"
		exec cmd "echo hello world"
		unplug dvd
		sleep 5s
	}
}

[no_snapshots: true]
test win10_test_ga_stop_service: win10_test_ga_install {
	win10 {
		press LeftMeta+R
		wait "Выполнить"
		press LeftAlt+LeftShift
		type "services.msc"; press Enter
		wait "Службы (локальные)"
		mouse click "DNS-клиент"
		press Down*20
		mouse dclick "Testo Guest Additions"
		wait "Свойства: Testo Guest Additions"
		exec cmd """type "C:\\Program Files\\Testo Lang\\Testo Guest Additions\\testo-guest-additions.txt""""
		mouse click "Остановить".from_left(1)
		wait "Остановлена"
	}
}

[no_snapshots: true]
test win10_test_ga_uninstall: win10_test_ga_install {
	win10 {
		press LeftMeta+X
		sleep 5s
		mouse click "Приложения и возможности"
		mouse click "Искать в этом списке"
		sleep 5s
		press LeftAlt+LeftShift
		sleep 5s
		type "Testo"
		press Down*3
		mouse click "Закрыть".from_bottom(0)
		sleep 5s
		mouse click "Testo Guest Additions"
		sleep 5s
		mouse click "Удалить".from_bottom(0).center_bottom()
		wait "Это приложение и все его"
		mouse click "Удалить".from_bottom(1).center_bottom()
		if (NOT check "Разрешить этому приложению" timeout 5s) {
			press LeftAlt+Tab
		}
		wait "Разрешить этому приложению"
		press Left, Enter
		wait "Приложения и возможности" && "Сортировать по" && !"Testo Guest Additions"
	}
}
