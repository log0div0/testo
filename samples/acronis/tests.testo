network nat {
	mode: "nat"
}

machine win7 {
	cpus: 2
	ram: 2Gb
	iso: "${ISO_DIR}/win7.iso"
	disk_size: 20Gb
	nic nat: {
		attached_to: "nat"
		adapter_type: "e1000"
	}
}

macro click_lowermost(text, index="0", x_shift="+0", y_shift="+0") {
	mouse click js """
		arr = detect_text('${text}')
		if (arr.length) {
			arr.sort(function (a, b) {
				if (a.y < b.y) {
					return 1
				}

				if (a.y > b.y) {
					return -1
				}

				return 0
			});

			obj = arr[${index}]
			target = {'x': obj.x${x_shift}, 'y': obj.y${y_shift}}
		} else {
			arr
		}
	""" timeout 2m
}

macro click_topmost(text, index="0", x_shift="+0", y_shift="+0") {
	mouse click js """
		arr = detect_text('${text}')
		if (arr.length) {
			arr.sort(function (a, b) {
				if (a.y > b.y) {
					return 1
				}

				if (a.y < b.y) {
					return -1
				}

				return 0
			});

			obj = arr[${index}]
			target = {'x': obj.x${x_shift}, 'y': obj.y${y_shift}}
		} else {
			arr
		}
	""" timeout 2m
}

macro click_leftmost(text, index="0", x_shift="+0", y_shift="+0") {
	mouse click js """
		arr = detect_text('${text}')
		if (arr.length) {
			arr.sort(function (a, b) {
				if (a.x < b.x) {
					return -1
				}

				if (a.x > b.x) {
					return 1
				}

				return 0
			});

			obj = arr[${index}]
			target = {'x': obj.x${x_shift}, 'y': obj.y${y_shift}}
		} else {
			arr
		}
	""" timeout 2m
}

test win7_installation {
	win7 {
		start
		wait "Choose an operating system"
		press Down, Enter

		mouse click "Мой язык - русский" timeout 2m;

		wait "Выберите нужный язык"; click_lowermost("Далее") #2 "Далее"
		mouse click "Установить" timeout 1m;
		wait "Выберите операционную систему"; press Down*8; mouse click "Далее"
		#wait "Выберите операционную систему"; mouse click "Далее"
		wait "Условия лицензии"
		mouse click "Я принимаю условия лицензии" timeout 1m; mouse click "Далее"
		wait "Выберите тип установки";
		if (check "Полная установка") {
			mouse click "Полная установка"
		} else {
			mouse click "Выборочная"
		}
		wait "Где вы хотите установить Windows?" || "Выберите раздел для установки Windows"; mouse click "Далее"
		wait "Выберите имя пользователя" timeout 15m
		unplug dvd
		press LeftShift + LeftAlt; type "testo"; mouse click "Далее"
		wait "Установите пароль"; click_lowermost("Далее")
		wait "Введите ключ продукта"; mouse click "Автоматически активировать Windows"; mouse click "Далее"
		wait "Помогите автоматически защитить компьютер"; mouse click "Отложить решение"
		wait "Проверьте настройку даты и времени"; mouse click "Далее"
		wait "Выберите текущее место расположения компьютера"; mouse click "Общественная сеть"
		wait "Корзина" timeout 10m
		shutdown
		start
		wait "Корзина"
	}
}


test guest_additions: win7_installation {
	win7 {
		plug dvd "${ISO_DIR}/testo-guest-additions.iso"
		mouse click "Выполнить testo-guest-additions" timeout 1m
		wait "Вас приветствует мастер установки"; mouse click "Далее"
		wait "Выбор папки для установки"; click_lowermost("Далее")
		wait "Подтверждение установки"; click_lowermost("Далее")
		wait "Разрешить внесение изменений"; press Left, Enter
		wait "Не удалось проверить издателя этих драйверов"; mouse click "Все равно установить"

		wait "Установка завершена"; click_lowermost("Закрыть")
		wait !"Установка завершена"
		exec cmd "echo hello world"
	}
}


flash acronis_distrib {
	size: 1Gb
	fs: "ntfs"
	folder: "${ACRONIS_DIR}"
}

test prepare: guest_additions {
	win7 {
		press LEFTMETA
		mouse click "Панель управления"
		mouse click "Категория"
		mouse click "Мелкие значки"
		mouse click "Учетные записи"
		mouse click "Изменение параметров контроля учетных записей"
		sleep 2s
		mouse dclick js "found = detect_text('Никогда')[0]; target = {x: found.x, y: found.y - 50}"
		click_lowermost("ОК")
		wait "Microsoft Windows"; press Left, Enter
		wait "Внесение изменений в учетную запись"
		press LeftAlt+F4
		wait "Корзина"

		mouse rclick 400 200
		mouse click "Разрешение экрана"
		mouse click "800"
		mouse click "1024 x 768"
		mouse dclick "Применить"
		mouse click "Сохранить изменения"
		press LeftAlt+F4
		wait "Корзина"

	}
}

test install_acronis: prepare {
	win7 {
		plug flash acronis_distrib
		mouse click "Открыть папку"
		wait "AcronisTrueImage2020"
		press LeftCtrl+A, LeftCtrl+C
		press LeftAlt+F4
		press LeftCtrl+V
		wait "Копирование"
		wait !"Копирование" timeout 5m
		unplug flash acronis_distrib

		mouse dclick "Acronis"
		mouse click "Установить" timeout 5m
		wait "Windows Internet Explorer" timeout 10m; press LeftAlt+F4
		wait "Установка завершена"; press LeftAlt+F4
		wait !"Установка завершена";
	}
}

test activate_acronis: install_acronis {
	win7 {
		mouse dclick "Activation"
		mouse dclick "Patch"
		mouse click "Поиск: Activation"; press LeftAlt+F4
		mouse click "Make Backup"
		click_lowermost("Patch")
		wait "Can not find the file"; press Enter

		sleep 2s
		click_leftmost("Activation", "1", "+100", "+0")
		
		press LeftShift + LeftAlt
		wait "C:\\Users"; press Backspace
		type "C:\\Program Files (x86)\\Acronis\\TrueImageHome"; press Enter
		click_topmost("ti_managers.dll"); press Enter

		wait "Can not find the file"; press Enter
		sleep 2s
		click_topmost("TrueImageTools"); press Enter

		wait "Can not find the file"; press Enter
		sleep 2s
		click_leftmost("TrueImageHome", "0", "+100", "+0")
		
		wait "C:\\Program Files"; press Backspace
		type "C:\\Program Files (x86)\\Common Files\\Acronis\\TrueImageHome"; press Enter
		click_topmost("TrueImageHomeService"); press Enter
		wait "PATCHING DONE"; mouse click "Exit"
		wait !"PATCHING DONE"
	}
}

flash acronis_backup {
	size: 6Gb
	fs: "ntfs"
}

test make_backup: activate_acronis {
	win7 {
		mouse dclick "Acronis True"
		mouse click "Я принимаю это соглашение"
		click_lowermost("OK")
		wait "Полное резервное" timeout 3m
		mouse click "Больше не показывать"; mouse click "Пропустить"
		press LeftAlt + Space
		mouse click "Развернуть"
		plug flash acronis_backup
		mouse click "Выбор"
		mouse click "NTFS, 6ГБ"
		mouse click "Создать копию"
		wait "Резервное копирование успешно завершено" timeout 30m
		unplug flash acronis_backup
		sleep 5s
	}
}
