include "../common_win7.testo"

network nat {
	mode: "nat"
}

machine win7 {
	cpus: 2
	ram: 2Gb
	iso: "${ISO_DIR}/win7.iso"
	disk_size: 20Gb
	nic nat: {
		attached_to: "nat"
		adapter_type: "e1000"
	}
}


test win7_installation {
	win7 install_win7("testo")
}


test guest_additions: win7_installation {
	win7 install_guest_additions()
}


flash acronis_distrib {
	size: 1Gb
	fs: "ntfs"
	folder: "${ACRONIS_DIR}"
}

[no_snapshots: true]
test prepare: guest_additions {
	win7 {
		press LEFTMETA
		mouse click "Панель управления"
		mouse click "Категория"
		mouse click "Мелкие значки"
		mouse click "Учетные записи"
		mouse click "Изменение параметров контроля учетных записей"
		mouse dclick "Никогда".move_up(50);
		mouse click "ОК".from_bottom(0);
		wait "Microsoft Windows"; press Left, Enter
		wait "Внесение изменений в учетную запись"
		press LeftAlt+F4
		wait "Корзина"

		mouse rclick 400 200
		mouse click "Разрешение экрана"
		mouse click "800"
		mouse click "1024 x 768"
		mouse dclick "Применить"
		mouse click "Сохранить изменения"
		press LeftAlt+F4
		wait "Корзина"

	}
}

test install_acronis: prepare {
	win7 {
		plug flash acronis_distrib
		mouse click "Открыть папку"
		wait "AcronisTrueImage2020"
		press LeftCtrl+A, LeftCtrl+C
		press LeftAlt+F4
		press LeftCtrl+V
		wait "Копирование"
		wait !"Копирование" timeout 5m
		unplug flash acronis_distrib

		mouse dclick "Acronis"
		mouse click "Установить" timeout 5m
		wait "Windows Internet Explorer" timeout 10m; press LeftAlt+F4
		wait "Установка завершена"; press LeftAlt+F4
		wait !"Установка завершена";
	}
}


test activate_acronis: install_acronis {
	win7 {
		mouse dclick "Activation"
		mouse dclick "Patch"
		mouse click "Поиск: Activation"; press LeftAlt+F4
		mouse click "Make Backup"
		mouse click "Patch".from_bottom(0)
		wait "Can not find the file"; press Enter

		wait "Открыть"
		mouse click "Activation".from_left(1).right_center().move_right(30)
		
		press LeftShift + LeftAlt
		wait "C:\\Users"; press Backspace
		type "C:\\Program Files (x86)\\Acronis\\TrueImageHome"; press Enter
		mouse click "ti_managers.dll".from_top(0); press Enter
		wait "Can not find the file"; press Enter
		wait "Открыть"
		mouse click "TrueImageTools".from_top(0); press Enter

		wait "Can not find the file"; press Enter
		wait "Открыть"
		mouse click "TrueImageHome".from_left(0).right_center().move_right(30)
		
		wait "C:\\Program Files"; press Backspace
		type "C:\\Program Files (x86)\\Common Files\\Acronis\\TrueImageHome"; press Enter
		mouse click "TrueImageHomeService".from_top(0); press Enter
		wait "PATCHING DONE"; mouse click "Exit"
		wait !"PATCHING DONE"
	}
}

flash acronis_backup {
	size: 6Gb
	fs: "ntfs"
}

test make_backup: activate_acronis {
	win7 {
		mouse dclick "Acronis True"
		mouse click "Я принимаю это соглашение"
		mouse click "OK".from_bottom(0)
		wait "Полное резервное" timeout 3m
		mouse click "Больше не показывать"; mouse click "Пропустить"
		press LeftAlt + Space
		mouse click "Развернуть"
		plug flash acronis_backup
		mouse click "Выбор"
		mouse click "NTFS, 6ГБ"
		mouse click "Создать копию"
		wait "Резервное копирование успешно завершено" timeout 30m
		unplug flash acronis_backup
		sleep 5s
	}
}
