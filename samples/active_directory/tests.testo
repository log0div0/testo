include "win_server.testo"
include "workstation.testo"

test connect_to_domain: win_server_add_user, workstation_install_win7 {
	workstation {
		sleep 5s

		if (check "Выберите расположение" timeout 10s) {
			mouse click "Общественная сеть"
			wait "Расположение сети изменилось"
			mouse click "Закрыть"
			wait !"Расположение сети изменилось"
		}

		press LeftMeta
		wait "Все программы"
		mouse rclick "Компьютер"
		mouse click "Свойства"

		mouse click "Изменить"
		mouse click "Изменить..."

		mouse click "домена:"
		mouse move 0 0
		mouse click "домена:".center_bottom().move_down(10)
		press LeftAlt + LeftShift
		type "${domain_name}"; press Enter
		wait "Использовать это нестандартное имя"; press Enter
		wait "Введите имя и пароль учетной записи"; type "${ad_user_login}";
		press Tab; type "${ad_user_default_password}"; press Enter

		wait "Добро пожаловать в домен"; press Enter
		wait "нужно перезагрузить компьютер"; press Enter

		mouse click "Закрыть".from_bottom(0)

		mouse click "Перезагрузить сейчас"

		wait "CTRL + ALT + DEL" timeout 3m
		press LeftCtrl + LeftAlt + Delete
		wait "Пароль"
		mouse click "RU"
		mouse click "Английский"
		wait !"Английский"

		mouse click "Сменить пользователя"
		mouse click "Другой пользователь"
		sleep 500ms

		type "${ad_user_login}"; press Tab; type "${ad_user_default_password}"; press Enter

		wait "Корзина" timeout 2m

		sleep 5s
		press LeftMeta; wait "Все программы"; mouse rclick "Компьютер"
		mouse click "Свойства"
		wait "${domain_name}"
	}
}