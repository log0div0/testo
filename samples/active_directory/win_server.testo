include "common.testo"

machine win_server {
	cpus: 2
	ram: 4096Mb
	iso: "${ISO_DIR}/14393.0.161119-1705.RS1_REFRESH_SERVER_EVAL_X64FRE_RU-RU.ISO"
	disk main: {
		size: 30Gb
	}

	nic LAN: {
		attached_to: "net1"
	}
}

param win_password "ThisIsStrongPassword1!"
param win_server_LAN_ip "192.168.1.1"
param win_dhcp_start_addr "192.168.1.2"
param win_dhcp_end_addr "192.168.1.254"
param win_server_hostname "ADController"

param ad_user_name "Foo"
param ad_user_lastname "Bar"

test win_server_os_installation {
	win_server {
		start
		wait "Выберите нужный язык" timeout 5m; mouse click "Далее".from_bottom(0)
		mouse click "Установить"

		wait "Выберите операционную систему";
		mouse click "Windows Server 2016 Standard Evaluation (возможности ра"
		mouse click "Далее".from_bottom(0)
		mouse click "Я принимаю условия лицензии"; mouse click "Далее".from_bottom(0)

		mouse click "только установка Windows"
		wait "Где вы хотите установить Windows?"; mouse click "Далее".from_bottom(0)
		wait "Введите пароль встроенной учетной записи администратора" timeout 20m;

		sleep 2s; type "${win_password}"
		press Tab; sleep 2s; type "${win_password}"; press Enter

		wait "CTRL+ALT+DELETE"
		press LeftCtrl + LeftAlt + Delete
		wait "Администратор"
		type "${win_password}"; press Enter
		wait "Вас приветствует диспетчер серверов" timeout 2m
	}
}

macro run_command(command) {
	press LeftMeta + R;
	wait "Введите имя программы";
	type "${command}"; press Enter
}

test win_server_os_prepare: win_server_os_installation {
	win_server {
		mouse click "РУС".from_bottom(0)
		mouse click "Английский"
		wait !"Английский"
		run_command("devmgmt.msc")
		wait "Диспетчер устройств"

		mouse dclick "USB-устройство ввода"

		# Переходим на вкладку "Power Management"
		# и снимаем галочку с пункта "Allow the computer to turn off ..."
		mouse click "Управление электропитанием"
		mouse click "Разрешить отключение этого устройства"
		sleep 3s

		# Закрываем окно свойств устройства "USB Input Device"
		press Enter
		wait !"Управление электропитанием"

		# Закрываем окно "Device Manager"
		press LeftAlt+F4
		wait !"Диспетчер устройств"

		mouse move 0 0
		run_command("control")
		wait "Панель управления"
		mouse click "Категория"; mouse click "Мелкие значки"

		mouse click "Система"
		mouse click "Изменить"

		wait "Свойства системы"
		mouse click "Изменить..."

		wait "Изменение имени компьютера"
		type "${win_server_hostname}";
		press Enter
		wait "нужно перезагрузить компьютер"; press Enter
		mouse click "Закрыть".from_bottom(0)
		mouse click "Перезагрузить сейчас"

		wait "CTRL+ALT+DELETE" timeout 30m
		press LeftCtrl + LeftAlt + Delete
		wait "Администратор"
		type "${win_password}"; press Enter
		wait "Вас приветствует диспетчер серверов" timeout 2m

		sleep 10s

		mouse click "РУС".from_bottom(0)
		mouse click "Английский"
		wait !"Английский"
		run_command("control")

		mouse click "Центр управления сетями"
		mouse click "Изменение параметров"
		wait "Ethernet"
		mouse rclick "Ethernet".from_top(0)
		mouse click "Свойства"
		mouse click "TCP/IPv4"
		mouse click "Свойства".from_bottom(0)
		mouse click "Использовать следующий IP-адрес"
		mouse click "IP-адрес".from_bottom(0).right_center().move_right(150)
		sleep 1s
		type "${win_server_LAN_ip}"; press Tab, Enter
		wait !"Использовать следующий IP-адрес"
		mouse click "Закрыть".from_bottom(0)
		wait !"свойства"
		press LeftAlt + F4
		wait !"Сетевые подключения"
		press LeftAlt + F4
		wait !"Центр управления сетями"
	}
}

test win_server_configure_ad: win_server_os_prepare {
	win_server {
		mouse click "Добавить роли и компоненты".from_right(0)
		wait "Перед началом работы"; mouse click "Далее".from_bottom(0).center_bottom()
		wait "Выбор типа установки"; mouse click "Далее".from_bottom(0).center_bottom()
		wait "Выбор целевого сервера"; mouse click "Далее".from_bottom(0).center_bottom()

		wait "Выбор ролей сервера";
		mouse dclick "Доменные службы Active Directory"
		mouse click "Добавить компоненты".from_bottom(0)
		mouse dclick "DNS-сервер"
		mouse click "Добавить компоненты".from_bottom(0)
		mouse dclick "DHCP-сервер"
		mouse click "Добавить компоненты".from_bottom(0)
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "Выбор компонентов"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "Доменные службы Active Directory"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "DNS-сервер"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "DHCP-сервер"
		mouse click "Далее".from_bottom(0).center_bottom()

		wait "Чтобы установить на выбранном сервере следующие роли"
		mouse click "Установить".from_bottom(0)

		wait "Повысить роль этого сервера до уровня контроллера домена" timeout 10m
	}
}

test win_server_configure_deployment: win_server_configure_ad {
	win_server {
		mouse click "Повысить роль этого сервера до уровня контроллера домена"
		wait "Конфигурация развертывания"
		mouse click "Добавить новый лес"
		mouse click "Имя корневого домена".right_center().move_right(200)
		type "${domain_name}"
		press Enter
		wait "Выберите режим работы нового леса"
		mouse click "Пароль".from_bottom(0).right_center().move_right(200);
		type "${win_password}"; press Tab; type "${win_password}"
		press Enter
		wait "Укажите параметры делегирования DNS"
		press Enter
		wait "NetBIOS-имя" && "MYDOMAIN"
		press Enter
		wait "Укажите расположение базы данных"
		press Enter
		wait "Просмотрите выбранные параметры"
		press Enter
		wait "Все проверки готовности к установке выполнены успешно"
		mouse click "Установить".from_bottom(0).center_bottom()
		wait "CTRL+ALT+DELETE" timeout 30m
		press LeftCtrl + LeftAlt + Delete
		wait "Администратор"
		type "${win_password}"; press Enter
		wait "Вас приветствует диспетчер серверов" timeout 2m
	}
}

test win_server_add_user: win_server_configure_deployment {
	win_server {
		mouse click "РУС".from_bottom(0)
		mouse click "Английский"
		wait !"Английский"
		mouse click "Средства".from_right(0)
		mouse click "Пользователи и компьютеры Active Directory"
		wait "${domain_name}"
		mouse click "${domain_name}".from_left(0)
		sleep 1s
		mouse dclick
		sleep 200ms
		mouse rclick "Users".from_left(0)
		mouse move "Создать".from_left(0)
		sleep 1s
		mouse click "Пользователь".from_bottom(0)
		wait "Новый объект - Пользователь"
		mouse click "Имя".from_top(0).right_center().move_right(100); type "${ad_user_name}"
		mouse click "Фамилия".right_center().move_right(100); type "${ad_user_lastname}"
		mouse click "Имя входа пользователя".from_bottom(1).center_bottom().move_down(10); type "${ad_user_login}"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "Пароль"; type "${ad_user_default_password}"; press Tab; type "${ad_user_default_password}"
		mouse click "Требовать смены пароля"
		mouse click "Далее".from_bottom(0).center_bottom()
		mouse click "Готово".from_bottom(0)
		sleep 1s
		press LeftAlt + F4
		wait !"${domain_name}"
	}
}

test win_configure_dhcp: win_server_add_user {
	win_server {
		#mouse click "Manage".from_right(0).left_center().move_left(20)
		mouse click "DHCP".from_left(0)
		wait "DHCP-сервер - требуется настройка"
		mouse click "Подробнее"
		mouse click "Завершение настройки DHCP"

		wait "Для завершения настройки DHCP"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "Укажите учетные данные"
		mouse click "Фиксировать".center_bottom()
		mouse click "Закрыть"

		wait "Сведения о задаче"
		press LeftAlt + F4
		wait !"Сведения о задаче"

		mouse click "Средства".from_right(0)
		mouse click "DHCP".from_right(0)

		mouse dclick "${domain_name}"
		mouse dclick "IPv4".from_right(0)
		mouse rclick "IPv4".from_left(0)
		mouse click "Создать область"
		wait "Мастер создания области"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "Необходимо обеспечить уникальное имя области"
		type "SCOPE1"
		mouse click "Далее".from_bottom(0).center_bottom()
		wait "Определить диапазон адресов области"
		type "${win_dhcp_start_addr}"; press Tab
		type "${win_dhcp_end_addr}"; press Enter
		wait "Добавление исключений"; press Enter
		wait "Срок действия аренды адреса"; press Enter
		wait "Настройка параметров DHCP"; press Enter
		wait "основной шлюз"; type "${win_server_LAN_ip}"; mouse click "Добавить".from_right(0); sleep 200ms; press Enter
		wait "DNS-серверы"; press Enter
		wait "WINS-серверы"; press Enter
		wait "Активировать область"; press Enter
		wait "Завершение мастера"; press Enter
		wait !"Завершение мастера"
	}
}
