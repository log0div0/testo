
macro select_menu_horizontal(menu_entry) {
	wait "${menu_entry}"

	#check if it's enabled by default
	if (check js """detect_text("${menu_entry}", "gray", "red")""") {
		press Enter
	} else {
		press Left*5

		for i in 0..5 {
			if (check js """detect_text("${menu_entry}", "gray", "red")""" timeout 50ms) {
				press Enter;
				break;
			}

			press Right
		} else {
			abort "Can't find menu entry ${menu_entry}"
		}
	}
}

macro select_menu_vertical(menu_entry) {
	wait "${menu_entry}"

	#check if it's enabled by default
	if (check js """detect_text("${menu_entry}", "gray", "red")""") {
		press Enter
	} else {
		press Up*20

		for i in 0..20 {
			if (check js """detect_text("${menu_entry}", "gray", "red")""" timeout 50ms)  {
				press Enter;
				break;
			}

			press Down
		} else {
			abort "Can't find menu entry ${menu_entry}"
		}
	}
}

macro enter_sudo(host_name, login, password) {
	type "sudo su"; press Enter;
	wait "root@${host_name}"
}

macro install_guest_additions(host_name, login, password) {
	enter_sudo("${host_name}", "${login}", "${password}")
	plug dvd "${ISO_DIR}/testo-guest-additions.iso"; wait timeout 5s
	type "mount /dev/cdrom /media"; press Enter
	wait "mounting read-only"; type "dpkg -i /media/*.deb"; press Enter;
	wait "Настраивается пакет testo-guest-additions"
	type "umount /media"; press Enter;
	wait timeout 2s
	unplug dvd
}
