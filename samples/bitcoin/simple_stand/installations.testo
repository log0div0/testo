
include "../../common_ubuntu_server.testo"
include "../../common_ubuntu_desktop.testo"

network nat {
	mode: "nat"
}

network net1 {
	mode: "internal"
}

param default_password "1111"
param miner_host_name "miner"
param miner_login "miner"

machine miner {
	cpus: 2
	ram: 1024Mb
	iso: "${ISO_DIR}/ubuntu-20.04.1-live-server-amd64.iso"

	disk main: {
		size: 20Gb
	}

	nic internet: {
		attached_to: "nat"
		adapter_type: "e1000"
	}

	nic LAN: {
		attached_to: "net1"
		adapter_type: "e1000"
	}
}

param bitcoin_client_login "client"
param bitcoin_client_host_name "client"

machine bitcoin_client {
	cpus: 2
	ram: 8Gb
	iso: "${ISO_DIR}/ubuntu-20.04.1-desktop-amd64.iso"
	disk main: {
		size: 20Gb
	}

	nic internet: {
		attached_to: "nat"
	}

	nic LAN: {
		attached_to: "net1"
		adapter_type: "e1000"
	}
}

test miner_install_ubuntu {
	miner {
		unplug nic internet
		start
		if (check "Language" timeout 5s) {
			press Enter
			wait "Install Ubuntu Server"; press Enter
		}
		wait "Welcome!" timeout 5m; press Enter

		wait "Please select your keyboard layout"; press Enter
		wait "Configure at least one interface"; press Enter
		wait "Proxy address"; press Enter
		wait "Mirror address"; press Enter
		wait "Continue without updating"; press Enter
		wait "Use an entire disk"; press Down*5; wait js "find_text().match('Done').match_background('green').size() > 0"; press Enter
		wait "FILE SYSTEM SUMMARY"; press Enter
		wait "Confirm destructive action"; press Down, Enter
		wait "Enter the username"; type "${miner_login}"; press Tab
		type "${miner_host_name}"; press Tab
		type "${miner_login}"; press Tab
		type "${default_password}"; press Tab
		type "${default_password}"; press Enter*2
		wait "SSH Setup"; press Tab, Enter
		wait "Installing system"
		wait "Installation complete!" timeout 15m
		press Enter
		wait "Please remove the installation medium"
		unplug dvd; press Enter
		wait "${miner_host_name} login" timeout 3m

		type "${miner_login}"; press Enter
		wait "Password"; type "${default_password}"; press Enter

		wait "${miner_login}@${miner_host_name}"
	}
}

test miner_install_ga: miner_install_ubuntu {
	miner {
		shutdown
		plug nic internet
		start
		wait "${miner_host_name} login" timeout 3m

		type "${miner_login}"; press Enter
		wait "Password"; type "${default_password}"; press Enter

		wait "${miner_login}@${miner_host_name}"
	}
	miner install_guest_additions_ubuntu_server("${miner_host_name}", "${miner_login}", "${default_password}")
}

test miner_prepare: miner_install_ga {
	miner {
		exec bash "ip l s ens7 up"
		exec bash "dhclient ens7"

		exec bash "hwclock --hctosys "

		exec bash """
			add-apt-repository ppa:luke-jr/bitcoincore
			apt-get update
			apt-get install bitcoind -y
		""" timeout 20m

		exec bash "apt purge -y netplan.io"

		exec bash "ip l s ens8 up"
		exec bash "ip a a 192.168.1.1/24 dev ens8"
	}
}


test miner_test_bitcoin: miner_prepare {
	miner {
		exec bash "mkdir -p /root/.bitcoin"
		copyto "../misc/bitcoin.conf" "/root/.bitcoin/bitcoin.conf"

		exec bash "bitcoind -datadir=/root/.bitcoin/ --daemon"

		sleep 5s

		exec bash "bitcoin-cli -datadir=/root/.bitcoin/ -regtest getwalletinfo | grep walletname"
		exec bash "bitcoin-cli -datadir=/root/.bitcoin/ -regtest getnewaddress > /root/my_wallet_address.txt"
		exec bash "bitcoin-cli -datadir=/root/.bitcoin/ -regtest generatetoaddress 1 `(cat /root/my_wallet_address.txt)`"
		exec bash """
			bitcoin-cli -datadir=/root/.bitcoin/ -regtest getwalletinfo | grep "\\\"immature_balance\\\": 50.0"
		"""
	}
}

test client_install_ubuntu {
	bitcoin_client install_ubuntu20_desktop("${bitcoin_client_host_name}", "${bitcoin_client_login}", "${default_password}")
}

test client_install_configure_and_install_ga: client_install_ubuntu {
	bitcoin_client {
		mouse click "Skip".center_bottom();
		wait "Livepatch"; mouse click "Next".from_right(0).center_bottom()
		wait "Help improve Ubuntu"; mouse click "Next".from_right(0).center_bottom()
		wait "Privacy"; mouse click "Next".from_right(0).center_bottom()
		wait "Ready to go"; mouse click "Done".from_right(0).center_bottom()
		run_app_ubuntu20_desktop("Settings"); mouse click "Power"
		mouse click "5 minutes"
		mouse click "15 minutes"; sleep 500ms
		mouse click "15 minutes"; mouse click "Never"

		mouse click "Power".from_left(0); press Down
		mouse move 0 0; mouse click "Displays"
		mouse click "800 x 600"; mouse click "1920 x 1080"
		mouse click "Apply".from_right(0)
		mouse click "Keep Changes"
		wait !"Keep Changes"
		press LeftAlt + F4; wait !"Power"
	}
	bitcoin_client install_ga_ubuntu20_desktop("${bitcoin_client_host_name}", "${bitcoin_client_login}", "${default_password}")
}

test client_prepare: client_install_configure_and_install_ga {
	bitcoin_client {
		exec bash "hwclock --hctosys "

		exec bash """
			add-apt-repository ppa:luke-jr/bitcoincore
			apt-get update
			apt-get install bitcoin-qt -y
		"""

		exec bash "apt purge -y netplan.io"

		exec bash "ip l s ens8 up"
		exec bash "ip a a 192.168.1.2/24 dev ens8"
	}
}

test client_create_address: client_prepare {
	bitcoin_client {
		exec bash "mkdir /home/client/.bitcoin"
		exec bash "chown -R client /home/client/.bitcoin"

		copyto "../misc/bitcoin.conf" "/home/client/.bitcoin/bitcoin.conf"

		run_app_ubuntu20_desktop("Bitcoin")

		wait "Bitcoin Core - [regtest]"
		mouse click "Hide".from_bottom(0)
		mouse click "Receive".from_top(0)
		mouse click "Create new receiving address"
		mouse click "Copy Address"
		mouse click "Close"

		run_app_ubuntu20_desktop("gedit")
		wait "Untitled Document 1"
		press LeftCtrl + V
		press LeftCtrl + S
		wait "Other Locations"
		type "client_address.txt"; press Enter
		wait !"Other Locations"
		press LeftAlt + F4
		sleep 2s
	}
}

flash exchange_flash {
	fs: "vfat"
	size: 8Mb
}

test basic_transaction: miner_test_bitcoin, client_create_address {
	bitcoin_client {
		plug flash exchange_flash
		sleep 3s
		exec bash """
			mount /dev/sdb1 /media
			cp /home/client/client_address.txt /media/
			cat /media/client_address.txt
			umount /media
		"""
		sleep 3s
		unplug flash exchange_flash
	}
	miner {
		plug flash exchange_flash
		sleep 3s
		exec bash """
			mount /dev/sdb1 /media
			cp /media/client_address.txt /root/
			umount /media
		"""
		unplug flash exchange_flash
		exec bash "bitcoin-cli -datadir=/root/.bitcoin/ -regtest generatetoaddress 100 `(cat /root/my_wallet_address.txt)`"
		exec bash "bitcoin-cli -datadir=/root/.bitcoin/ -regtest sendtoaddress `(cat /root/client_address.txt)` 10"
		exec bash "bitcoin-cli -datadir=/root/.bitcoin/ -regtest generatetoaddress 1 `(cat /root/my_wallet_address.txt)`"
	}

	bitcoin_client {
		mouse click "Overview"
		wait "Available: 10.00"
	}
}
