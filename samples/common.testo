
macro install_ubuntu(host_name, login, password) {
	start
	wait timeout 2s
	wait "Language";
	wait timeout 2s;
	press Enter;
	wait "Install Ubuntu Server"; press Enter;
	wait "Choose the language";	press Enter
	wait "Select your location"; press Enter
	wait "Detect keyboard layout?";	press Enter
	wait "Country of origin for the keyboard"; press Enter
	wait "Keyboard layout"; press Enter
	wait "No network interfaces detected" || "Primary network interface" timeout 5m
	if (check "No network interfaces detected") {
		press Right, Enter
	} else {
		press Enter
	}
	wait "Hostname:" timeout 30s; press Backspace*36; type "${host_name}"; press Enter
	wait "Full name for the new user"; type "${login}"; press Enter
	wait "Username for your account"; press Enter
	wait "Choose a password for the new user"; type "${password}"; press Enter
	wait "Re-enter password to verify"; type "${password}"; press Enter
	wait "Use weak password?"; press Left, Enter
	wait "Encrypt your home directory?"; press Enter
	wait "Select your time zone" || "Moscow" timeout 2m; press Enter
	wait "Partitioning method"; press Enter
	wait "Select disk to partition"; press Enter
	wait "Write the changes to disks and configure LVM?"; press Left, Enter
	wait "Amount of volume group to use for guided partitioning"; press Enter
	wait "Write the changes to disks?"; press Left, Enter
	wait "HTTP proxy information" timeout 3m; type "${HTTP_PROXY}"; press Enter
	wait "How do you want to manage upgrades" timeout 6m; press Enter
	wait "Choose software to install"; press Enter
	wait "Install the GRUB boot loader to the master boot record?" timeout 10m; press Enter
	wait "Installation complete" timeout 1m; unplug dvd; press Enter
	wait "login:" timeout 2m; type "${login}"; press Enter
	wait "Password:"; type "${password}"; press Enter
	wait "Welcome to Ubuntu"
}

macro enter_sudo(host_name, login, password) {
	type "sudo su"; press Enter;
	wait "password for ${login}"; type "${password}"; press Enter
	wait "root@${host_name}"
}

macro install_guest_additions(host_name, login, password) {
	enter_sudo("${host_name}", "${login}", "${password}")
	type "apt-get update"; press Enter
	wait "Reading package lists... Done" timeout 5m;
	type "clear && apt-get install -y python daemon"; press Enter
	wait "Setting up daemon" timeout 3m
	wait "root@${host_name}"

	plug dvd "${ISO_DIR}/negotiator.iso"; wait timeout 5s
	type "mount /dev/cdrom /media"; press Enter
	wait "mounting read-only"; type "dpkg -i /media/negotiator-agent-0.0.1.deb"; press Enter;
	wait "Setting up negotiator-agent"
}

macro unplug_nat(login, password) {
	shutdown

	unplug nic nat
	start

	wait "login:"; type "${login}"; press Enter
	wait "Password:"; type "${password}"; press Enter
	wait "Welcome to Ubuntu"
}

