

macro install_ubuntu20_desktop(host_name, login, password) {
	start
	if (check "Language" timeout 10s) {
		wait "Language"; press Enter
		wait "Try Ubuntu without installing"
		press Down*2, Enter
		wait "Welcome" timeout 3m
		mouse click "Continue".from_right(0).center_bottom()
	} else {
		wait "Welcome" timeout 3m
		mouse click "Install Ubuntu".from_right(0).center_bottom()
	}
	wait "Keyboard layout"
	mouse click "Continue".center_bottom()
	wait "Updates and other software"
	mouse click "Minimal installation"
	mouse click "Continue".from_right(0).center_bottom()
	wait "Installation type"
	mouse click "Install Now".from_right(0).center_bottom()
	wait "Write the changes to disks?"
	mouse click "Continue".from_right(0).center_bottom()

	wait "Where are you?"
	mouse click "Continue".from_right(0).center_bottom()

	wait "Who are you?"
	type "${login}"; press Tab; press LeftCtrl + A; press Backspace; type "${host_name}"
	mouse click "Choose a password".right_center().move_right(30)
	type "${password}"; press Tab; type "${password}"
	mouse click "Log in automatically"
	mouse click "Continue".from_right(0).center_bottom()

	wait "Installation Complete" timeout 30m
	mouse click "Restart Now"

	wait "Please remove the installation medium"; unplug dvd;
	sleep 3s; press Enter

	wait "Activities" timeout 5m
}

macro enter_sudo_ubunbtu20_desktop(host_name, login, password) {
	type "sudo su"; press Enter;
	wait "password for ${login}"; type "${password}"; press Enter
	wait "root@${host_name}"
}

macro run_app_ubuntu20_desktop(app) {
	mouse click "Activities"
	mouse click "Type to search"; type "${app}";
	sleep 2s
	press Enter
}

macro install_ga_ubuntu20_desktop(host_name, login, password, iso_dir = "${ISO_DIR}") {
	run_app_ubuntu20_desktop("Terminal")
	wait "${login}@${host_name}"
	plug dvd "${iso_dir}/testo-guest-additions.iso"

	enter_sudo_ubunbtu20_desktop("${host_name}", "${login}", "${password}")

	type "mount /dev/cdrom /media"; press Enter
	wait "device write-protected"
	type "rm -rf /var/lib/dpkg/lock"; press Enter
	type "rm -rf /var/lib/dpkg/lock-frontend"; press Enter
	type "dpkg -i /media/testo-guest-additions.deb"; press Enter
	wait "Processing triggers for systemd"
	exec bash "echo hello world"

	exec bash "umount /media"
	unplug dvd
	sleep 2s
}