macro install_win7(login) {
	start
	wait "Choose an operating system"
	press Down, Enter

	mouse click "Мой язык - русский" timeout 2m;

	wait "Выберите нужный язык"; mouse click "Далее".from_bottom(0)
	mouse click "Установить" timeout 1m;
	wait "Выберите операционную систему"; press Down*8; mouse click "Далее"
	#wait "Выберите операционную систему"; mouse click "Далее"
	wait "Условия лицензии"
	mouse click "Я принимаю условия лицензии" timeout 1m; mouse click "Далее"
	wait "Выберите тип установки";
	if (check "Полная установка") {
		mouse click "Полная установка"
	} else {
		mouse click "Выборочная"
	}
	wait "Где вы хотите установить Windows?" || "Выберите раздел для установки Windows"; mouse click "Далее".center_bottom()
	wait "Выберите имя пользователя" timeout 15m
	unplug dvd
	press LeftShift + LeftAlt; type "${login}"; mouse click "Далее".center_bottom()
	wait "Установите пароль"; mouse click "Далее".from_bottom(0).center_bottom()
	wait "Введите ключ продукта"; mouse click "Автоматически активировать Windows"; mouse click "Далее".center_bottom()
	wait "Помогите автоматически защитить компьютер"; mouse click "Отложить решение"
	wait "Проверьте настройку даты и времени"; mouse click "Далее".center_bottom()
	wait "Выберите текущее место расположения компьютера"; mouse click "Общественная сеть"
	wait "Корзина" timeout 10m
	shutdown
	start
	wait "Корзина"
}


macro install_guest_additions_win7() {
	sleep 5s

	plug dvd "${ISO_DIR}/testo-guest-additions.iso"
	mouse click "Выполнить autorun.js" timeout 1m
	wait "Вас приветствует мастер установки"; mouse click "Далее"
	wait "Выбор папки для установки"; mouse click "Далее".from_bottom(0)
	wait "Подтверждение установки"; mouse click "Далее".from_bottom(0)
	wait "Разрешить внесение изменений"; press Left, Enter
	mouse click "Установить".from_top(1)

	wait "Установка завершена"; mouse click "Закрыть".from_bottom(0)
	wait !"Установка завершена"
	exec cmd "echo hello world"
}