
param cus_id "150"
param ub_id "250"

network cus_ub {
	mode: "internal"
}

network cus_client {
	mode: "internal"
}

machine CUS {
	cpus: 1
	ram: 2048Mb
	iso: "${ISO_DIR}/Continent.iso"
	disk_size: 20Gb

	nic UB_side: {
		slot: 0
		attached_to: "cus_ub"
		mac: "52:54:00:00:00:AA"
	}

	nic client_side: {
		slot: 0
		attached_to: "cus_client"
		mac: "52:54:00:00:00:BB"
	}
}

machine UB {
	cpus: 1
	ram: 2048Mb
	iso: "${ISO_DIR}/Continent.iso"
	disk_size: 20Gb

	nic UB_side: {
		slot: 0
		attached_to: "cus_ub"
		mac: "52:54:00:00:11:AA"
	}
}

machine client {
	cpus: 2
	ram: 2Gb
	iso: "${ISO_DIR}/win7.iso"
	disk_size: 20Gb
	nic nat: {
		attached_to: "cus_client"
		adapter_type: "e1000"
	}
}

macro install_continent(id) {
	start
	wait "Please select"; press Up, Enter
	wait "Установить Континент"; press Enter
	wait "Выберите тип платформы"; press Enter
	wait "Введите идентификатор шлюза"; type "${id}"; press Enter*2
	wait "Please select" timeout 15m; stop; unplug dvd; start
	wait "Главное меню" timeout 10m
}

test install_CUS {
	CUS {
		install_continent("${cus_id}")
	}
}

test install_UB {
	UB {
		install_continent("${ub_id}")
	}
}

macro select_local_menu(string) {
	wait "${string}"
	for (i IN RANGE "20") {
		if (check js "detect_text('${string}', 'blue', 'gray')" timeout 50ms) {
			press Enter;
			break;
		}

		press Down
	} else {
		abort "Can't find menu entry ${string}"
	}
}

macro select_local_menu_horizontal(string) {
	wait "${string}"
	for (i IN RANGE "5") {
		if (check js "detect_text('${string}', 'blue', 'gray')" timeout 50ms) {
			press Enter;
			break;
		}

		press Right
	} else {
		abort "Can't find menu entry ${string}"
	}
}

test init_CUS: install_CUS {
	CUS {
		select_local_menu("Инициализация");
		wait "Инициализировать устройство как"; press Up*2; select_local_menu("Начать инициализацию")
		wait "Успешно" timeout 5m; press Enter;
		select_local_menu("Сертификаты");
		select_local_menu("Сертификаты УЦ")
		wait "Кому выдан"; press F2
		select_local_menu("Выпуск корневого сертификата")
		wait "Страна";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "CA"; press Enter
		wait "Успешно"; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Кому выдан"; press Esc
		select_local_menu("Сертификаты управления")
		wait "Кому выдан"; press F2
		select_local_menu("Выпуск сертификата управления для ЦУС")
		wait "Страна"; press Down*3; type "Control"; press Enter
		wait "Выберите корневой сертификат"; select_local_menu("CA")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Кому выдан"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Настройка ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выбор сертификата управления"; select_local_menu("Control")
		wait "Пароль для встроенного администратора"; type "Cont-4.X"; press Down; type "Cont-4.X"; press Enter
		wait "Выберите интерфейс управления"; select_local_menu("fe-0-0")
		wait "Настройка интерфейса управления"; type "192.168.0.1/24"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 15m; press Enter
	}
}

test init_UB: install_UB {
	UB {
		select_local_menu("Инициализация");
		wait "Инициализировать устройство как"; press Enter
		wait "Успешно" timeout 6m; press Enter;
		wait "Главное меню";
	}
}

flash ub_flash {
	fs: "ntfs"
	size: 16Mb
}

test config_UB: init_CUS, init_UB {
	#CUS init
	UB {
		select_local_menu("Сертификаты")
		select_local_menu("Запросы на выпуск сертификата")
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub_flash
		sleep 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB-${ub_id}"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub_flash
	}

	CUS {
		plug flash ub_flash
		select_local_menu("Вход в систему")
		select_local_menu_horizontal("Учетная запись")
		wait "Вход в систему"
		type "admin"; press Tab
		type "Cont-4.X"; press Enter
		select_local_menu("Сертификаты")
		select_local_menu("Сертификаты управления")
		wait "F2 - Выпуск серт."; press F2
		select_local_menu("Выпуск сертификата управления для узла безопасности")
		wait "Есть ли запрос для создания сертификата?"; select_local_menu_horizontal("Да")
		wait "Выберите Запрос"; select_local_menu("continent-${ub_id}.req")
		wait "Выберите корневой сертификат"; select_local_menu("CA")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Инструменты")
		select_local_menu("Создать Узел Безопасности")
		wait "Введите ID узла безопасности"; type "${ub_id}"; press Enter
		wait "Выбор сертификата управления"; select_local_menu("UB-${ub_id}")
		wait "Узел безопасности ${ub_id} успешно создан" timeout 2m; press Enter
		select_local_menu("Экспорт конфигурации УБ на носитель")
		wait "Выгрузить политику"; type "${ub_id}"; press Enter
		wait "Успешно"; press Enter
		sleep 10s;
		unplug flash ub_flash
	}

	UB {
		plug flash ub_flash
		sleep 5s
		select_local_menu("Подключиться к ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выберите конфигурацию"; select_local_menu("gate-${ub_id}.json")
		wait "Выберите интерфейс управления"; select_local_menu("fe-0-0")
		wait "Настройка интерфейса управления"; type "192.168.0.2/24"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 10m; press Enter
		unplug flash ub_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}

macro click_lowermost(text) {
	mouse click js """
		arr = detect_text('${text}')
		if (arr.length) {
			arr.sort(function (a, b) {
				return a.Y < b.Y
			});

			arr[arr.length - 1]
		} else {
			arr
		}
	""" timeout 2m
}

test client_install_win7 {
	client {
		start
		wait "Choose an operating system"
		press Down, Enter

		mouse click "Мой язык - русский" timeout 2m;

		wait "Выберите нужный язык"; click_lowermost("Далее") #2 "Далее"
		mouse click "Установить" timeout 1m;
		wait "Выберите операционную систему"; press Down*8; mouse click "Далее"
		#wait "Выберите операционную систему"; mouse click "Далее"
		wait "Условия лицензии"
		mouse click "Я принимаю условия лицензии" timeout 1m; mouse click "Далее"
		wait "Выберите тип установки";
		if (check "Полная установка") {
			mouse click "Полная установка"
		} else {
			mouse click "Выборочная"
		}
		wait "Где вы хотите установить Windows?" || "Выберите раздел для установки Windows"; mouse click "Далее"
		wait "Выберите имя пользователя" timeout 15m
		unplug dvd
		press LeftShift + LeftAlt; type "testo"; mouse click "Далее"
		wait "Установите пароль"; click_lowermost("Далее")
		wait "Введите ключ продукта"; mouse click "Автоматически активировать Windows"; mouse click "Далее"
		wait "Помогите автоматически защитить компьютер"; mouse click "Отложить решение"
		wait "Проверьте настройку даты и времени"; mouse click "Далее"
		wait "Выберите текущее место расположения компьютера"; mouse click "Общественная сеть"
		wait "Корзина" timeout 10m
		shutdown
		start
		wait "Корзина"
	}
}

test client_install_guest_additions: client_install_win7 {
	client {
		sleep 5s

		plug dvd "${ISO_DIR}/testo-guest-additions.iso"
		mouse click "Выполнить testo-guest-additions" timeout 1m
		wait "Вас приветствует мастер установки"; mouse click "Далее"
		wait "Выбор папки для установки"; click_lowermost("Далее")
		wait "Подтверждение установки"; click_lowermost("Далее")
		wait "Разрешить внесение изменений"; press Left, Enter
		wait "Не удалось проверить издателя этих драйверов"; mouse click "Все равно установить"

		wait "Установка завершена"; click_lowermost("Закрыть")
		wait !"Установка завершена"
		exec cmd "echo hello world"
	}
}

test client_configure_windows: client_install_guest_additions {
	client {
		press LEFTMETA
		mouse click "Панель управления"
		mouse click "Категория"
		mouse click "Мелкие значки"
		mouse click "Учетные записи"
		mouse click "Изменение параметров контроля учетных записей"
		mouse dclick js "found = detect_text('Никогда')[0]; target = {x: found.x, y: found.y - 50}"
		click_lowermost("ОК")
		wait "Microsoft Windows"; press Left, Enter
		wait "Внесение изменений в учетную запись"
		press LeftAlt+F4
		wait "Корзина"
	}
}

test client_install_cryptopro: client_configure_windows {
	client {
		copyto "./CSP/CSPSetup.exe" "C:\\Users\\testo\\Desktop\\CSPSetup.exe"
		abort "stop here"
	}
}