
include "installations.testo"

test config_UB: init_CUS, init_UB {
	#CUS init
	CUS {
		cont_login("admin", "${default_password}")
		select_local_menu("Настройки")
		select_local_menu("Сеть")
		select_local_menu("Сетевые интерфейсы")
		select_local_menu("ge-1-0")
		wait "IP/Mask"
		type "${cus_ub_side_ip}/${default_netmask}"; press Enter
		wait "Имеются неприменённые локальные изменения"
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Применение локальной политики")
		wait "Успешно" timeout 5m; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Имеются неподтверждённые изменения с узлов"
		press up; select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения с node-${cus_id}"; press Space, Enter
		wait "Подтверждено конфигураций"; press Enter
		press up; select_local_menu("Возврат в предыдущее меню")
		wait "Главное меню"
	}

	UB {
		select_local_menu("Сертификаты")
		select_local_menu("Запросы на выпуск сертификата")
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub_flash
		sleep 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB-${ub_id}"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub_flash
	}

	CUS {
		plug flash ub_flash
		select_local_menu("Сертификаты")
		select_local_menu("Сертификаты управления")
		wait "F2 - Выпуск серт."; press F2
		select_local_menu("Выпуск сертификата управления для узла безопасности")
		wait "Есть ли запрос для создания сертификата?"; select_local_menu_horizontal("[ Да ]")
		wait "Выберите Запрос"; select_local_menu("continent-${ub_id}.req")
		wait "Выберите корневой сертификат"; select_local_menu("CA (")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Инструменты")
		select_local_menu("Создать Узел Безопасности")
		wait "Введите ID узла безопасности"; type "${ub_id}"; press Enter
		wait "Выбор сертификата управления"; select_local_menu("UB-${ub_id}")
		wait "Узел безопасности ${ub_id} успешно создан" timeout 2m; press Enter
		select_local_menu("Экспорт конфигурации УБ на носитель")
		wait "Выгрузить политику"; type "${ub_id}"; press Enter
		wait "Успешно"; press Enter
		sleep 10s;
		unplug flash ub_flash
	}

	UB {
		plug flash ub_flash
		select_local_menu("Подключиться к ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выберите конфигурацию"; select_local_menu("gate-${ub_id}.json")
		wait "Выберите интерфейс управления"; select_local_menu("ge-0-0")
		wait "Настройка интерфейса управления"; type "${ub_cus_side_ip}/${default_netmask}"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 10m; press Enter
		unplug flash ub_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}

	UB {
		cont_login("admin", "${default_password}")
		select_local_menu("Настройки")
		select_local_menu("Сеть")
		select_local_menu("Сетевые интерфейсы")
		select_local_menu("ge-1-0") #WIN7_CLIENT_side
		wait "IP/Mask"
		type "${ub_win7_side_ip}/${default_netmask}"; press Enter

		select_local_menu("ge-2-0") # UBUNTU_CLIENT_side
		wait "IP/Mask"
		type "${ub_ubuntu_client_side_ip}/${default_netmask}"; press Enter

		select_local_menu("ge-3-0") # UBUNTU_SERVER_side
		wait "IP/Mask"
		type "${ub_ubuntu_server_side_ip}/${default_netmask}"; press Enter

		select_local_menu("Возврат в предыдущее меню")
		press Up
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Применение локальной политики")
		wait "Успешно" timeout 2m; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Главное меню"
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}

[no_snapshots: true]
test win7_client_connect_CUS: win7_client_install_ms, config_UB {
	win7_client {
		press LeftAlt + LeftShift
		sleep 5s
		press LEFTMETA; wait "Все программы"
		type "cmd"; press Enter
		wait "cmd.exe"
		press LeftAlt + LeftShift
		type "netsh interface ip set address name=\""
		press LeftAlt + LeftShift
		type "Подключение по локальной сети"
		press LeftAlt + LeftShift
		type "\" static ${win7_cus_side_ip} ${win7_client_netmask} ${cus_main_ip}"
		press Enter
		type "exit"; press Enter
		mouse click "Общественная сеть"; mouse click "Закрыть"
		mouse dclick "Менеджер"
		wait "Подключение к ЦУС"
		press LeftAlt + LeftShift
		mouse click "Сервер".move_right(150)
		type "${cus_main_ip}"
		mouse click "Учётная запись".move_right(150)
		type "admin"
		mouse click "Пароль".move_right(150)
		type "${default_password}"
		mouse click "Подключить"
		wait "Конфигурация системы заблокирована"; mouse click "Восстановить"; mouse click "Продолжить".from_bottom(0)
		wait "${cus_main_ip} - Континент"
	}
}

[no_snapshots: true]
test install_allow_all_policy: win7_client_connect_CUS {
	win7_client {
		press LeftAlt + Space
		mouse click "Развернуть"
		mouse click "Первое правило"
		mouse click "Пропустить"
		mouse click "Установить"

		wait "Установить политики"
		mouse click "node-${ub_id}".move_left(65)
		mouse click "ОК".from_bottom(0)

		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет"
	}
}

test install_nf2: install_allow_all_policy {
	win7_client {
		mouse move "Список объектов ЦУС".left_center().move_left(5)
		mouse hold lbtn
		mouse move "ЦУС"
		mouse release
		mouse click "Структура"
		mouse rclick "node-${ub_id}"
		mouse click "Свойства".from_bottom(0)
		mouse click "UTM"
		mouse click "Высокопроизводитель"
		mouse click "Приоритизация трафика".from_right(0)
		mouse click "ОК".from_bottom(0)
		mouse click "Установить"
		wait "Установить политики"
		mouse click "ОК".from_bottom(0)
		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет"
	}

	UB {
		cont_login("admin", "${default_password}")
		wait "Для завершения смены режима требуется перезагрузка"
		select_local_menu("Завершение работы устройства")
		select_local_menu_horizontal("Перезагрузка")
		wait "Вход в систему" timeout 5m
		press LeftAlt + F2
		wait "node-${ub_id} login"
		type "root"; press Enter
		wait "Password"; type "${default_password}"; press Enter
		wait "root@node-${ub_id}";
		type "nf2 status"; press Enter
		wait "Running"
	}
}

[no_snapshots: true]
test check_avaliability: install_nf2, ubuntu_client_prepare, ubuntu_server_prepare {
	ubuntu_client sleep 10s
	ubuntu_client exec bash "ping -c5 ${ubuntu_server_ip}"
	ubuntu_server exec bash "ping -c5 ${ubuntu_client_ip}"

	#check ftp active/passive

	ubuntu_client {
		#active mode
		exec bash "curl -P - ftp://${ubuntu_server_ip}/ftp_test.txt"
		exec bash "curl --ftp-pasv ftp://${ubuntu_server_ip}/ftp_test.txt"
	}

	#check smb
	ubuntu_client {
		#active mode
		exec bash "curl -u \"guest:\" smb://${ubuntu_server_ip}/sambashare/samba_test.txt"
	}

}
