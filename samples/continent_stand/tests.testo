
machine CUS {
	cpus: 1
	ram: 2048Mb
	iso: $ISO_DIR + "/Continent.iso"
	disk_size: 20Gb

	nic UB_side: {
		slot: 0
		attached_to: "internal"
		network: "net1"
		mac: "52:54:00:00:00:AA"
	}

	metadata: {
		id: "150"
		login: "root"
		password: "1111"
	}
}

machine UB {
	cpus: 1
	ram: 2048Mb
	iso: $ISO_DIR + "/Continent.iso"
	disk_size: 20Gb

	nic UB_side: {
		slot: 0
		attached_to: "internal"
		network: "net1"
		mac: "52:54:00:00:00:BB"
	}

	metadata: {
		id: "250"
		login: "root"
		password: "1111"
	}
}

macro install_continent() {
	start
	wait timeout 3s
	wait "Установить Континент"; press Enter
	wait "Выберите тип платформы"; press Enter
	wait "Введите идентификатор шлюза"; type $id; press Enter*2
	wait "Установить Континент" timeout 10m; stop; unplug dvd; start
	wait "Главное меню" timeout 10m
}

test install_CUS {
	CUS {
		install_continent()
	}
}

test install_UB {
	UB {
		install_continent()
	}
}

macro select_local_menu(string) {
	wait $string
	for i in 0..20 {
		if ($i EQUAL "20") {
			abort "Can't find menu entry " + $string
		}

		if (check $string (foreground = "blue", background = "gray")) {
			press Enter;
			break;
		}

		press Down; wait timeout 1s
	}
}

macro select_local_menu_horizontal(string) {
	wait $string
	for i in 0..20 {
		if ($i EQUAL "20") {
			abort "Can't find menu entry " + $string
		}

		if (check $string (foreground = "blue", background = "gray")) {
			press Enter;
			break;
		}

		press Right; wait timeout 1s
	}
}

test init_CUS: install_CUS {
	CUS {
		select_local_menu("Инициализация");
		wait "Инициализировать устройство как"; press Up*2; select_local_menu("Начать инициализацию")
		wait "Очистить локальные журналы"; select_local_menu_horizontal("Да")
		wait "Успешно" timeout 5m; press Enter;
		select_local_menu("Сертификаты");
		select_local_menu("Сертификаты УЦ")
		wait "Кому выдан"; press F2
		select_local_menu("Выпуск корневого сертификата")
		wait "Страна";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "CA"; press Enter
		wait "Успешно"; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Кому выдан"; press Esc
		select_local_menu("Сертификаты управления")
		wait "Кому выдан"; press F2
		select_local_menu("Выпуск сертификата управления для ЦУС")
		wait "Страна"; press Down*3; type "Control"; press Enter
		wait "Выберите корневой сертификат"; select_local_menu("CA")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Кому выдан"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Настройка ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выбор сертификата управления"; select_local_menu("Control")
		wait "Пароль для встроенного администратора"; type "Cont-4.X"; press Down; type "Cont-4.X"; press Enter
		wait "Выберите интерфейс управления"; select_local_menu("fe-0-0")
		wait "Настройка интерфейса управления"; type "192.168.0.1/24"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 15m; press Enter
	}
}

test init_UB: install_UB {
	UB {
		select_local_menu("Инициализация");
		wait "Инициализировать устройство как"; press Enter
		wait "Очистить локальные журналы"; select_local_menu_horizontal("Да")
		wait "Успешно" timeout 6m; press Enter;
		wait "Главное меню";
	}
}

flash ub_flash {
	fs: "ntfs"
	size: 16Mb
}

test config_UB: init_CUS, init_UB {
	#CUS init
	UB {
		select_local_menu("Сертификаты")
		select_local_menu("Запросы на выпуск сертификата")
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub_flash
		wait timeout 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB-250"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub_flash
	}

	CUS {
		plug flash ub_flash
		select_local_menu("Вход в систему")
		select_local_menu_horizontal("Соболь")
		select_local_menu("Сертификаты")
		select_local_menu("Сертификаты управления")
		wait "F2 - Выпуск серт."; press F2
		select_local_menu("Выпуск сертификата управления для узла безопасности")
		wait "Есть ли запрос для создания сертификата?"; select_local_menu_horizontal("Да")
		wait "Выберите Запрос"; select_local_menu("continent-250.req")
		wait "Выберите корневой сертификат"; select_local_menu("CA")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Инструменты")
		select_local_menu("Создать узел безопасности")
		wait "Введите ID узла безопасности"; type "250"; press Enter
		wait "Выбор сертификата управления"; select_local_menu("UB-250")
		wait "Узел безопасности 250 успешно создан" timeout 2m; press Enter
		select_local_menu("Экспорт конфигурации УБ на носитель")
		wait "Выгрузить политику"; type "250"; press Enter
		wait "Успешно"; press Enter
		wait timeout 10s;
		unplug flash ub_flash
	}

	UB {
		plug flash ub_flash
		wait timeout 5s
		select_local_menu("Подключиться к ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выберите конфигурацию"; select_local_menu("gate-250.json")
		wait "Выберите интерфейс управления"; select_local_menu("fe-0-0")
		wait "Настройка интерфейса управления"; type "192.168.0.2/24"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 10m; press Enter
		unplug flash ub_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}