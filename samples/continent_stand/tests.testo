
machine CUS {
	cpus: 1
	ram: 2048Mb
	iso: $ISO_DIR + "/Continent.iso"
	disk_size: 20Gb

	nic UB_side: {
		slot: 0
		attached_to: "internal"
		network: "net1"
		mac: "52:54:00:00:00:AA"
	}

	metadata: {
		id: "150"
		login: "root"
		password: "1111"
	}
}

machine UB {
	cpus: 1
	ram: 2048Mb
	iso: $ISO_DIR + "/Continent.iso"
	disk_size: 20Gb

	nic UB_side: {
		slot: 0
		attached_to: "internal"
		network: "net1"
		mac: "52:54:00:00:00:BB"
	}

	metadata: {
		id: "250"
		login: "root"
		password: "1111"
	}
}

macro install_continent() {
	start
	wait timeout 3s
	wait "Установить Континент"; press Enter
	wait "Выберите тип платформы"; press Enter
	wait "Введите идентификатор шлюза"; type $id; press Enter*2
	#wait "Установить Континент" timeout 10m; stop; unplug dvd; start
	wait "Главное меню" timeout 10m
}

test install_CUS {
	CUS install_continent()
}

test install_UB {
	UB install_continent()
}


test init_CUS: install_CUS {
	CUS {
		press Down, Enter
		wait "Инициализировать устройство как"; press Up*2, Enter
		wait "Очистить локальные журналы"; press Enter
		wait "Успешно" timeout 5m; press Enter;
		wait "Главное меню"; press Down*2, Enter
		wait "Сертификаты УЦ"; press Enter
		wait "Кому выдан"; press F2
		wait "Выпуск сертификата"; press Enter
		wait "Страна";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "CA"; press Enter
		wait "Успешно"; press Enter
		wait timeout 1s; press Down, Enter
		wait "Кому выдан"; press Esc
		wait "Сертификаты управления"; press Down, Enter
		wait "Кому выдан"; press F2
		wait "Выпуск сертификата"; press Enter
		wait "Страна";
		press Down*3; type "Control"; press Enter
		wait "Выберите корневой сертификат"; press Enter
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Кому выдан"; press Esc
		wait "Сертификаты управления"; press Esc
		wait "Главное меню"; press Down, Enter
		wait "Все службы будут сконфигурированы заново"; press Enter
		wait "Выбор сертификата управления"; press Enter
		wait "Пароль для встроенного администратора"; type "Cont-4.X"
		press Down; type "Cont-4.X"; press Enter
		wait "Выберите интерфейс управления"; press Enter
		wait "Настройка интерфейса управления"; type "192.168.0.1/24"; press Enter
		wait "Применить указанные настройки?"; press Enter
		wait "Система успешно настроена" timeout 15m; press Enter
	}
}

test init_UB: install_UB {
	UB {
		press Down, Enter;
		wait "Инициализировать устройство как"; press Enter
		wait "Очистить локальные журналы"; press Enter
		wait "Успешно" timeout 5m; press Enter;
		wait "Главное меню";
	}
}

flash ub_flash {
	fs: "ntfs"
	size: 16Mb
	cache_enabled: 0
}

test config_UB: init_CUS, init_UB {
	#CUS init
	UB {
		press Down*2, Enter
		wait "Запросы на выпуск сертификата"; press Enter
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub_flash
		wait timeout 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub_flash
	}

	CUS {
		plug flash ub_flash
		wait "Вход в систему"; press Enter
		wait "Выбор метода аутентификации"; press Enter
		wait "Главное меню"; press Down; press Enter
		wait "Сертификаты управления"; press Down, Enter
		wait "F2 - Выпуск серт."; press F2
		wait "Выпуск сертификата"; press Down, Enter
		wait "Есть ли запрос для создания сертификата?"; press Enter
		wait "Выберите Запрос"; press Enter
		wait "Выберите корневой сертификат"; press Enter
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню"; press Down*2, Enter
		wait "Меню инструменты"; press Down*3; press Enter
		wait "Введите ID узла безопасности"; type "250"; press Enter
		wait "Выбор сертификата управления"; press Down, Enter
		wait "Узел 250 успешно создан" timeout 2m; press Enter
		wait "Меню инструменты"; press Down*7, Enter
		wait "Выгрузить политику"; type "250"; press Enter
		wait "Успешно"; press Enter
		wait timeout 1m;
		unplug flash ub_flash
	}

	UB {
		plug flash ub_flash
		wait timeout 5s
		press Down, Enter
		wait "Все службы будут сконфигурированы заново"; press Enter
		wait "Выберите конфигурацию"; press Down, Enter
		wait "Выберите интерфейс управления"; press Enter
		wait "Настройка интерфейса управления"; type "192.168.0.2/24"; press Enter
		wait "Применить указанные настройки?"; press Enter
		wait "Система успешно настроена" timeout 3m; press Enter
		unplug flash ub_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m; press Enter
		wait "Меню инструменты"; press Down*6, Enter
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter
	}
}