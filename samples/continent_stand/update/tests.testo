
include "installations.testo"

test config_UB: init_CUS, init_UB {
	#CUS init
	CUS {
		cont_login("admin", "${default_password}")
		select_local_menu("Настройки")
		select_local_menu("Сеть")
		select_local_menu("Сетевые интерфейсы")
		select_local_menu("ge-1-0")
		wait "IP/Mask"
		type "${cus_ub_side_ip}/${default_netmask}"; press Enter
		wait "Имеются неприменённые локальные изменения"
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Применение локальной политики")
		wait "Успешно" timeout 5m; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Имеются неподтверждённые изменения с узлов"
		press up; select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения с node-${cus_id}"; press Space, Enter
		wait "Подтверждено конфигураций"; press Enter
		press up; select_local_menu("Возврат в предыдущее меню")
		wait "Главное меню"
	}

	UB {
		select_local_menu("Сертификаты")
		select_local_menu("Запросы на выпуск сертификата")
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub_flash
		sleep 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB-${ub_id}"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub_flash
	}

	CUS {
		plug flash ub_flash
		select_local_menu("Сертификаты")
		select_local_menu("Сертификаты управления")
		wait "F2 - Выпуск серт."; press F2
		select_local_menu("Выпуск сертификата управления для узла безопасности")
		wait "Есть ли запрос для создания сертификата?"; select_local_menu_horizontal("[ Да ]")
		wait "Выберите Запрос"; select_local_menu("continent-${ub_id}.req")
		wait "Выберите корневой сертификат"; select_local_menu("CA (")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Инструменты")
		select_local_menu("Создать Узел Безопасности")
		wait "Введите ID узла безопасности"; type "${ub_id}"; press Enter
		wait "Выбор сертификата управления"; select_local_menu("UB-${ub_id}")
		wait "Узел безопасности ${ub_id} успешно создан" timeout 2m; press Enter
		select_local_menu("Экспорт конфигурации УБ на носитель")
		wait "Выгрузить политику"; type "${ub_id}"; press Enter
		wait "Успешно"; press Enter
		sleep 10s;
		unplug flash ub_flash
	}

	UB {
		plug flash ub_flash
		select_local_menu("Подключиться к ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выберите конфигурацию"; select_local_menu("gate-${ub_id}.json")
		wait "Выберите интерфейс управления"; select_local_menu("ge-0-0")
		wait "Настройка интерфейса управления"; type "${ub_cus_side_ip}/${default_netmask}"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 10m; press Enter
		unplug flash ub_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}

test win7_client_connect_CUS: win7_client_install_ms, config_UB {
	win7_client {
		press LeftAlt + LeftShift
		sleep 5s
		press LEFTMETA; wait "Все программы"
		type "cmd"; press Enter
		wait "cmd.exe"
		press LeftAlt + LeftShift
		type "netsh interface ip set address name=\""
		press LeftAlt + LeftShift
		type "Подключение по локальной сети"
		press LeftAlt + LeftShift
		type "\" static ${win7_cus_side_ip} ${win7_client_netmask}"
		press Enter
		type "exit"; press Enter
		#mouse click "Общественная сеть"; mouse click "Закрыть"
		mouse dclick "Менеджер"
		wait "Подключение к ЦУС"
		press LeftAlt + LeftShift
		mouse click "Сервер".move_right(150)
		type "${cus_main_ip}"
		mouse click "Учётная запись".move_right(150)
		type "admin"
		mouse click "Пароль".move_right(150)
		type "${default_password}"
		mouse click "Подключить"
		wait "Конфигурация системы заблокирована"; mouse click "Восстановить"; mouse click "Продолжить".from_bottom(0)
		wait "${cus_main_ip} - Континент"
	}
}

test load_update: win7_client_connect_CUS {
	win7_client {
		press LeftAlt + Space
		mouse click "Развернуть"
		sleep 1s
		mouse move "Список объектов ЦУС".left_center().move_left(5)
		mouse hold lbtn
		mouse move "ЦУС"
		mouse release
		mouse click "Администрирование"
		mouse click "Обновления"
		unplug dvd
		plug dvd "${ISO_DIR}/Continent/${UPDATE_BUILD_NUM}/Continent.iso"
		if (check "CD-дисковод" timeout 10s) {
			press LeftAlt + F4
		}
		mouse click "Импорт"
		mouse click "Компьютер".from_bottom(0)
		mouse dclick "Continent"
		mouse dclick "Continent-4.1.0-${UPDATE_BUILD_NUM}"
		wait "Загрузка файла"
		wait "Файл обновления загружен" timeout 10m; press Enter
		wait "4.1.0-${UPDATE_BUILD_NUM}"
	}
}

test CUS_update: load_update {
	win7_client {
		mouse rclick "node-${cus_id}"
		mouse click "Установить обновление на узел безопасности"
		wait "Установка обновления"
		mouse click "node-${cus_id}"
		mouse click "Обновление".from_bottom(0).right_center().move_right(480)
		sleep 2s
		mouse click "4.1.0-${UPDATE_BUILD_NUM}".from_right(0)
		mouse click "Установить".from_bottom(0)
		wait "Добавлена задача по установке обновления"; press Enter
		sleep 1s
		press LeftAlt + F4
		wait "Корзина"
	}

	CUS {
		wait "БД заблокирована текущим администратором"
		wait "Проверка целостности ПО и конфигурации завершена: ошибок не найдено" timeout 20m
		wait "Главное меню" timeout 5m
		cont_login("admin", "${default_password}")
		select_local_menu("Сведения")
		select_local_menu("Устройство")
		wait "Версия сборки: 4.1.0.${UPDATE_BUILD_NUM}"
	}
}

[no_snapshots: true]
test MS_update: CUS_update {
	win7_client {
		copyto "${ISO_DIR}/Continent/${UPDATE_BUILD_NUM}/MS/x64" "C:\\Users\\testo\\Desktop\\MS_update"
		mouse dclick "MS_update"
		sleep 1s
		mouse dclick "setup".from_right(0)
		wait "Эта программа установки выполнит обновление"; press Enter
		wait "Вас приветствует программа обновления" timeout 5m
		mouse click "Далее".from_bottom(0)
		wait "Программа завершена" timeout 5m
		mouse click "Готово".from_bottom(0)
		wait "требуется перезагрузка"; press Enter
		unplug dvd
		sleep 3s
		wait "Корзина" timeout 5m
		sleep 5s
	}
}

test MS_update_part2: MS_update {
	win7_client {
		mouse dclick "Менеджер"
		wait "Подключение к ЦУС"
		press LeftAlt + LeftShift
		mouse click "Сервер".move_right(150)
		type "${cus_main_ip}"
		mouse click "Учётная запись".move_right(150)
		type "admin"
		mouse click "Пароль".move_right(150)
		type "${default_password}"
		mouse click "Подключить"
		wait "Конфигурация системы заблокирована"; mouse click "Восстановить"; mouse click "Продолжить".from_bottom(0)
		wait "${cus_main_ip} - Континент"

		press LeftAlt + Space
		mouse click "Развернуть"
		sleep 1s
		mouse move "Список объектов ЦУС".left_center().move_left(5)
		mouse hold lbtn
		mouse move "ЦУС"
		mouse release

		mouse click "Встроенный администратор".right_center().move_right(37)
		wait "Сборка: 4.1.0.${UPDATE_BUILD_NUM}"; press Enter; sleep 1s

		mouse rclick "Нет элементов".from_top(0)
		mouse click "Создать первое правило"
		mouse click "Пропустить"
		mouse click "Установить"

		wait "Установить политики"
		mouse click "node-${cus_id}".move_left(65)
		mouse click "ОК".from_bottom(0)

		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет" timeout 3m
	}
}

[no_snapshots: true]
test UB_update: MS_update_part2 {
	win7_client {
		mouse click "Администрирование"
		mouse click "Обновления"
		mouse rclick "node-${ub_id}"
		mouse click "Установить обновление на узел безопасности"
		wait "Установка обновления"
		mouse click "node-${ub_id}"
		mouse click "Обновление".from_bottom(0).right_center().move_right(480)
		sleep 2s
		mouse click "4.1.0-${UPDATE_BUILD_NUM}".from_right(0)
		mouse click "Установить".from_bottom(0)
		wait "Добавлена задача по установке"; press Enter
	}

	UB {
		wait "Проверка целостности ПО и конфигурации завершена: ошибок не найдено" timeout 20m
		wait "Главное меню" timeout 5m
		cont_login("admin", "${default_password}")
		select_local_menu("Сведения")
		select_local_menu("Устройство")
		wait "Версия сборки: 4.1.0.${UPDATE_BUILD_NUM}"
	}

	win7_client {
		mouse click "Контроль доступа"
		mouse click "Установить"

		wait "Установить политики"
		mouse click "node-${ub_id}".move_left(65)
		#mouse click "ОК".from_bottom(0)
		press Enter

		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет" timeout 3m
	}
}