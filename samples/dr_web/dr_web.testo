include "os_installation.testo"

flash dr_web_flash {
	size: 2Gb
	fs: "ntfs"
	folder: "${DR_WEB_DIR}"
}

macro try_login() {
	for (i IN RANGE 30) {
		mouse click
		if (check("testo")) {
			break;
		}
		sleep 3s
	}
}

test dr_web_install: win10_prepare {
	win10 {
		plug flash dr_web_flash
		sleep 5s
		mouse click "USB Drive"
		mouse click "Open folder"
		mouse click "drweb"
		press LeftCtrl + C
		mouse click "Edge".move_down(50)
		press LeftCtrl + V
		wait "Copying"
		wait !"Copying" timeout 5m
		mouse click "USB Drive".from_top(0)
		press LeftAlt + F4
		unplug flash dr_web_flash
		sleep 3s
		mouse dclick "drweb"
		wait "Do you want to allow"; mouse click "Yes".from_bottom(0)
		wait "Thank you for choosing Dr.Web" timeout 3m
		mouse click "I want to connect to Dr.Web Cloud".right_center()
		mouse click "Install Dr.Web Firewall"
		mouse click "Next".from_bottom(0)
		mouse click "Receive license later"
		mouse click "Install"
		wait "Installation complete" timeout 10m
		mouse click "Restart now"
		wait "Please wait" timeout 3m
		try_login()
		type "1111"; press Enter
		wait "Type here to search" && "Dr.Web" && "Scanner" timeout 3m
	}
}

macro open_app(app) {
	mouse click "Type here to search"
	wait "Top apps"
	type "${app}"
	wait "Open"
	press Enter
}

test dr_web_prepare: dr_web_install {
	win10 {
		open_app("Security Center")
		wait "Computer is protected"
		mouse click "Click to make changes"
		wait "Do you want to allow"
		mouse click "Yes".from_bottom(0)
		wait "Computer is protected"
	}
}

flash access_denied {
	fs: "ntfs"
	size: 16Mb
}

test dr_web_flash_drive_restriction: dr_web_prepare {
	win10 {
		plug flash access_denied
		wait "USB Drive (E:)"
		mouse rclick "This folder is empty"
		mouse move "New"; sleep 3s
		mouse click "Text Document".from_top(1)
		wait "New Text Document";
		press Backspace; type "my_file"; press Enter
		mouse rclick "USB Drive (E:)".from_left(0); mouse click "Eject"
		wait "Safe To Remove Hardware"
		unplug flash access_denied

		mouse click "Devices and Personal Data"
		mouse click "Configure device access rules"
		mouse click "Block removable media"
		sleep 2s
		plug flash access_denied

		open_app("File Explorer")
		wait "Frequent folders"
		mouse click "USB Drive (E:)".from_left(0)
		wait "Access is denied"
		mouse click "OK".from_right(0)

		mouse rclick "USB Drive (E:)".from_left(0); mouse click "Eject"
		wait "Safe To Remove Hardware"
		unplug flash access_denied
		press LeftAlt + F4
		mouse click "Block removable media"
		plug flash access_denied
		wait "USB Drive (E:)" && "my_file"
		mouse rclick "my_file"
		mouse click "Delete"
		wait "Are you sure"; mouse click "Yes".from_right(0)
		mouse rclick "This folder is empty"
		mouse rclick "USB Drive (E:)".from_left(0); mouse click "Eject"
		wait "Safe To Remove Hardware"
		unplug flash access_denied
	}
}
