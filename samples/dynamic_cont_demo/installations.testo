include "declarations.testo"
include "routers_installations.testo"
include "arms_installations.testo"
include "ms_installations.testo"
include "continent_installations.testo"

flash ub1_flash {
	fs: "ntfs"
	size: 16Mb
}

test UB1_connect_to_CUS: UB1_init, CUS_init, router1_prepare {
	UB1 {
		select_local_menu("Сертификаты")
		select_local_menu("Запросы на выпуск сертификата")
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub1_flash
		sleep 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB-${UB1_id}"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub1_flash
	}

	CUS {
		cont_login("admin", "${default_password}")
		plug flash ub1_flash
		select_local_menu("Сертификаты")
		select_local_menu("Сертификаты управления")
		wait "F2 - Выпуск серт."; press F2
		select_local_menu("Выпуск сертификата управления для узла безопасности")
		wait "Есть ли запрос для создания сертификата?"; select_local_menu_horizontal("[ Да ]")
		wait "Выберите Запрос"; select_local_menu("continent-${UB1_id}.req")
		wait "Выберите корневой сертификат"; select_local_menu("CA (")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Инструменты")
		select_local_menu("Создать Узел Безопасности")
		wait "Введите ID узла безопасности"; type "${UB1_id}"; press Enter
		wait "Выбор сертификата управления"; select_local_menu("UB-${UB1_id}")
		wait "Узел безопасности ${UB1_id} успешно создан" timeout 2m; press Enter
		select_local_menu("Экспорт конфигурации УБ на носитель")
		wait "Выгрузить политику"; type "${UB1_id}"; press Enter
		wait "Успешно"; press Enter
		sleep 10s;
		unplug flash ub1_flash
	}

	UB1 {
		plug flash ub1_flash
		select_local_menu("Подключиться к ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выберите конфигурацию"; select_local_menu("gate-${UB1_id}.json")
		wait "Выберите интерфейс управления"; select_local_menu("ge-0-0")
		wait "Настройка интерфейса управления"; type "${UB1_OSPF1_ip}/${default_netmask}"; press Tab;
		type "${router1_OSPF1_ip}"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 10m; press Enter
		unplug flash ub1_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}

[no_snapshots: true]
test UB1_configure_network: UB1_connect_to_CUS {
	UB1 {
		cont_login("admin", "${default_password}")
		select_local_menu("Настройки")
		select_local_menu("Сеть")
		select_local_menu("Сетевые интерфейсы")
		select_local_menu("ge-1-0") #OSPF2_side
		wait "IP/Mask"; type "${UB1_OSPF2_ip}/${default_netmask}"; press Enter
		select_local_menu("ge-2-0") #BGP1_side
		wait "IP/Mask"; type "${UB1_BGP1_ip}/${BGP_netmask}"; press Enter
		select_local_menu("ge-3-0") #LAN3_side
		wait "IP/Mask"; type "${UB1_LAN3_ip}/${default_netmask}"; press Enter
		select_local_menu("Возврат в предыдущее меню")
		#Static routes
		select_local_menu("Статические маршруты")
		press Down*2; wait js "find_text().match('0.0.0.0').match_foreground('blue').match_background('gray').size() == 1"
		press Delete; wait "Удалить маршрут?"; select_local_menu_horizontal("[ Дa ]")
		wait !"0.0.0.0"

		press N; wait "Новый маршрут"; press Delete*10;
		type "${LAN1_ip}/${default_netmask}"; press Tab
		type "${router1_OSPF1_ip}"; press Enter
		wait "Пожалуйста, подождите";
		wait "${router1_OSPF1_ip}"

		press N; wait "Новый маршрут"; press Delete*10;
		type "${BGP2_ip}/${BGP_netmask}"; press Tab
		type "${router2_BGP1_ip}"; press Enter
		wait "Пожалуйста, подождите";
		wait "${router2_BGP1_ip}"
		press Esc; wait "Сетевые настройки"
		press Esc
		select_local_menu("Применение локальной политики")
		wait "Успешно" timeout 2m; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Главное меню"
	}

	CUS {
		wait "Инструменты"; press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}

test UB1_allow_all_policy: UB1_configure_network, MS_install_ms {
	MS {
		sleep 5s
		press LEFTMETA; wait "Все программы"
		press LeftAlt + LeftShift
		type "cmd"; press Enter
		wait "cmd.exe"
		press LeftAlt + LeftShift
		type "netsh interface ip set address name=\""
		press LeftAlt + LeftShift
		type "Подключение по локальной сети"
		press LeftAlt + LeftShift
		type "\" static ${MS_ip} ${MS_netmask}"
		press Enter
		type "exit"; press Enter
		mouse dclick "Менеджер"
		wait "Подключение к ЦУС"
		press LeftAlt + LeftShift
		mouse click "Сервер".move_right(150)
		type "${CUS_ip}"
		mouse click "Учётная запись".move_right(150)
		type "admin"
		mouse click "Пароль".move_right(150)
		type "${default_password}"
		mouse click "Подключить"
		wait "Конфигурация системы заблокирована"; mouse click "Восстановить"; mouse click "Продолжить".from_bottom(0)
		wait "${CUS_ip} - Континент" && !"Подключение к ЦУС"

		press LeftAlt + Space
		mouse click "Развернуть"
		mouse click "Первое правило"
		mouse click "Пропустить"
		mouse click "Установить"

		wait "Установить политики"
		mouse click "node-${UB1_id}".move_left(65)
		mouse click "ОК".from_bottom(0)

		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет"
	}
}

flash ub2_flash {
	fs: "ntfs"
	size: 16Mb
}

test UB2_connect_to_CUS: UB1_allow_all_policy, router2_prepare, UB2_init {
	UB2 {
		select_local_menu("Сертификаты")
		select_local_menu("Запросы на выпуск сертификата")
		wait "F4 - создать запрос"; press F4
		wait "Вставьте USB Flash накопитель"
		plug flash ub2_flash
		sleep 5s
		press Enter
		wait "Идёт создание запроса";
		press Down; type "Security Code"
		press Down; type "DevOps"
		press Down; type "UB-${UB2_id}"; press Enter
		wait "Запрос записан на носитель"; press Enter, Esc
		wait "Возврат в предыдущее меню"; press Esc
		wait "Главное меню";
		unplug flash ub2_flash
	}

	CUS {
		wait "БД заблокирована текущим администратором"; press Enter
		cont_login("admin", "${default_password}")
		plug flash ub2_flash
		select_local_menu("Сертификаты")
		select_local_menu("Сертификаты управления")
		wait "F2 - Выпуск серт."; press F2
		select_local_menu("Выпуск сертификата управления для узла безопасности")
		wait "Есть ли запрос для создания сертификата?"; select_local_menu_horizontal("[ Да ]")
		wait "Выберите Запрос"; select_local_menu("continent-${UB2_id}.req")
		wait "Выберите корневой сертификат"; select_local_menu("CA (")
		wait "Успешно"; press Enter
		wait "Выпуск сертификата"; press Esc
		wait "Сертификаты управления"; press Esc
		select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Инструменты")
		select_local_menu("Создать Узел Безопасности")
		wait "Введите ID узла безопасности"; type "${UB2_id}"; press Enter
		wait "Выбор сертификата управления"; select_local_menu("UB-${UB2_id}")
		wait "Узел безопасности ${UB2_id} успешно создан" timeout 2m; press Enter
		select_local_menu("Экспорт конфигурации УБ на носитель")
		wait "Выгрузить политику"; type "${UB2_id}"; press Enter
		wait "Успешно"; press Enter
		sleep 10s;
		unplug flash ub2_flash
	}

	UB2 {
		plug flash ub2_flash
		select_local_menu("Подключиться к ЦУС")
		wait "Все службы будут сконфигурированы заново"; select_local_menu_horizontal("Да")
		wait "Выберите конфигурацию"; select_local_menu("gate-${UB2_id}.json")
		wait "Выберите интерфейс управления"; select_local_menu("ge-0-0")
		wait "Настройка интерфейса управления"; type "${UB2_BGP2_ip}/${BGP_netmask}"; press Tab;
		type "${router2_BGP2_ip}"; press Enter
		wait "Применить указанные настройки?"; select_local_menu_horizontal("Да")
		wait "Система успешно настроена" timeout 10m; press Enter
		unplug flash ub2_flash
	}

	CUS {
		press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}	
}

[no_snapshots: true]
test UB2_configure_network: UB2_connect_to_CUS {
	UB2 {
		cont_login("admin", "${default_password}")

		select_local_menu("Настройки")
		select_local_menu("Сеть")
		select_local_menu("Сетевые интерфейсы")
		select_local_menu("ge-1-0") #LAN4_side
		wait "IP/Mask"; type "${UB2_LAN4_ip}/${default_netmask}"; press Enter
		select_local_menu("Возврат в предыдущее меню")
		press Up; select_local_menu("Возврат в предыдущее меню")
		select_local_menu("Применение локальной политики")

		wait "Успешно" timeout 2m; press Enter
		select_local_menu("Возврат в предыдущее меню")
		wait "Главное меню"

	}

	CUS {
		wait "Инструменты"; press Esc
		wait "Имеются неподтвержденные изменения с узлов" timeout 4m;
		select_local_menu("Инструменты")
		select_local_menu("Подтверждение изменений настроек УБ")
		wait "Локальные изменения"; press Space, Enter
		wait "Подтверждено конфигураций :1" timeout 1m; press Enter;
		wait "Инструменты"
	}
}

test UB2_install_allow_all: UB2_configure_network {
	MS {
		mouse click "Управление конфигурацией ЦУС захвачено"; press Enter
		mouse click "Файл";
		mouse click "Установить соединение"
		wait "Подключение к ЦУС"
		type "${default_password}"; mouse click "Подключить"
		wait "Конфигурация системы заблокирована"; mouse click "Восстановить"; mouse click "Продолжить".from_bottom(0)
		wait "${CUS_ip} - Континент" && !"Подключение к ЦУС"

		mouse click "Установить"

		wait "Установить политики"
		mouse click "node-${UB2_id}".move_left(65)
		mouse click "ОК".from_bottom(0)

		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет"
	}
}

test UB_loopbacks: UB2_install_allow_all {
	MS {
		mouse move "Список объектов ЦУС".left_center().move_left(5)
		mouse hold lbtn
		mouse move "ЦУС"
		mouse release
		mouse click "Структура"
		mouse rclick "node-${UB1_id}"
		mouse click "Свойства".from_bottom(0)
		mouse click "Интерфейсы"
		mouse rclick "ge-3-0".center_bottom().move_down(10)
		mouse click "Создать Loopback-интерфейс"
		mouse rclick "Не определён".from_bottom(0).right_center().move_right(20)
		mouse click "Insert"
		wait "Ввод адреса"
		type "${UB1_Lo0_ip}/${default_loopback_netmask}"; press Enter
		mouse rclick "lo0".center_bottom().move_down(10)
		mouse click "Создать Loopback-интерфейс"
		mouse rclick "Не определён".from_bottom(0).right_center().move_right(20)
		mouse click "Insert"
		wait "Ввод адреса"
		type "${UB1_Lo1_ip}/${default_loopback_netmask}"; press Enter
		mouse click "ОК".from_bottom(0)

		mouse rclick "node-${UB2_id}"
		mouse click "Свойства".from_bottom(0)
		mouse click "Интерфейсы"
		mouse rclick "ge-1-0".center_bottom().move_down(10)
		mouse click "Создать Loopback-интерфейс"
		mouse rclick "Не определён".from_bottom(0).right_center().move_right(20)
		mouse click "Insert"
		wait "Ввод адреса"
		type "${UB2_Lo0_ip}/${default_loopback_netmask}"; press Enter
		mouse rclick "lo0".center_bottom().move_down(10)
		mouse click "Создать Loopback-интерфейс"
		mouse rclick "Не определён".from_bottom(0).right_center().move_right(20)
		mouse click "Insert"
		wait "Ввод адреса"
		type "${UB1_Lo1_ip}/${default_loopback_netmask}"; press Enter
		mouse click "ОК".from_bottom(0)

		mouse click "Установить"

		wait "Установить политики"
		mouse click "ОК".from_bottom(0)

		wait "Установка политики на узлы безопасности"
		wait "Новых уведомлений нет"
	}
}

