include "ubuntu.testo"
include "windows.testo"

[no_snapshots: true]
test launch_game: win7_install_vcmi, ubuntu_install_vcmi {
	ubuntu {
		copyto "${VCMI_DIR}/VCMI_data/Maps" "/home/ubuntu/.local/share/vcmi/Maps"
		run_app_ubuntu20_desktop("vcmi launcher")
		mouse click img "icons/ubuntu/VCMI_launch.png"
		mouse click img "icons/new_game.png"
		mouse click img "icons/online_game.png"
		mouse click img "icons/server.png"
		wait "ЗА ОДНИМ КОМПЬЮТЕРОМ"
		press Backspace*6; type "Ubuntu"; press Enter
		wait "НАЗВАНИЕ СЦЕНАРИЯ"
		mouse click "Вилларибо и Вилладжо".from_left(0)
	}

	win7 {
		copyto "${VCMI_DIR}/VCMI_data/Maps" "C:\\Program Files (x86)\\VCMI (branch develop)\\Maps"
		mouse move 0 0
		mouse dclick "VCMI".from_bottom(0)
		wait "Download failed"; press Enter
		mouse click img "icons/VCMI_launch.png"
		mouse click img "icons/new_game.png"
		mouse click img "icons/online_game.png"
		mouse click img "icons/join_game.png"
		wait "ЗА ОДНИМ КОМПЬЮТЕРОМ"
		press Backspace*6; press LeftAlt + LeftShift; type "Windows"; press Enter
		wait "Enter address"
		press Backspace*20; type "${ubuntu_ip}"
		press Enter
		wait "НАЗВАНИЕ СЦЕНАРИЯ"
	}

	ubuntu {
		wait "system: Windows (pid cid 2) joins the game"
		mouse click "Дополнительные настройки"
		mouse click img "icons/change_bonus_red.png"
		wait img "icons/resources.png"
		mouse click
		wait img "icons/gold.png"
	}

	win7 {
		mouse click img "icons/change_bonus_blue.png"
		wait img "icons/resources.png"
	}

	ubuntu {
		mouse click "НАЧАТЬ"
		wait img "icons/necro_logo.png"
	}

	win7 wait img "icons/castle_logo.png"
}

macro collect_peasants() {
	mouse move 100 100
	wait "Вы хотите нанять Крестьяне?"
	mouse click homm3 "ok"
	wait "25 Крестьяне примкнули"
	mouse click homm3 "ok"
}

macro end_day_red() {
	mouse click img "icons/end_turn_red.png"
	if (check "еще могут перемещаться" timeout 2s) {
		mouse click homm3 "ok"
	}
}

macro end_day_blue() {
	mouse click img "icons/end_turn_blue.png"
	if (check "еще могут перемещаться" timeout 2s) {
		mouse click homm3 "ok"
	}
}

[no_snapshots: true]
test day1: launch_game {
	ubuntu {
		mouse dclick img "icons/crossroad_1.png"; sleep 2s
		mouse dclick homm3 "redwood_observatory".from_left(0);
		sleep 500ms; mouse move 200 50
		wait "Наблюдательная вышка"; mouse click homm3 "ok"
		mouse dclick homm3 "sign".from_top(0)
		wait "Виларибо"; mouse click homm3 "ok"
		mouse dclick homm3 "hovel".from_right(0); collect_peasants()

		mouse dclick img "icons/necro_logo.png"
		mouse click homm3 "hall"
		mouse click "Проклятый храм"
		mouse click img "icons/purchase.png"
		wait homm3 "cursed_temple"
		mouse click homm3 "ok"

		end_day_red()
	}

	win7 {
		wait img "icons/new_day.png"
		mouse dclick homm3 "redwood_observatory"
		wait "Наблюдательная вышка"; mouse click homm3 "ok"
		mouse dclick homm3 "town".center_bottom().move_up(10)
		mouse click homm3 "hall"
		mouse click "1 этаж гильдии магов"
		mouse click img "icons/purchase.png"
		mouse click homm3 "mage_guild"
		wait "волшебную книгу за 500 золотых"; press Enter
		wait homm3 "spell_slow"
		mouse click homm3 "ok"
		wait homm3 "mage_guild"
		mouse click homm3 "ok"
		end_day_blue()
	}
	ubuntu {
		wait img "icons/new_day.png"
	}
}

macro collect_knowledge() {
	wait "+1 Знания";
	mouse click homm3 "ok"
}

macro collect_spell() {
	wait "+1 Сила магии";
	mouse click homm3 "ok"
}

[no_snapshots: true]
test day2: day1 {
	ubuntu {
		mouse dclick homm3 "hovel".from_left(4); collect_peasants()
		mouse dclick homm3 "hovel".from_left(4); collect_peasants()
		mouse dclick homm3 "hovel".from_left(3); collect_peasants()
		mouse dclick homm3 "hovel".from_left(2); collect_peasants()
		mouse dclick homm3 "hovel".from_left(1); collect_peasants()
		mouse dclick homm3 "hovel".from_left(0); collect_peasants()

		mouse dclick img "icons/necro_logo.png"
		mouse click homm3 "hall"

		mouse click "Машина скелетов"
		mouse click img "icons/purchase.png"
		wait img "icons/skelet_machine_built.png";
		mouse click homm3 "ok"

		end_day_red()
	}

	win7 {
		wait img "icons/new_day.png"
		mouse dclick homm3 "scholar".from_left(0)
		wait "Мудрость"; press Enter; sleep 1s
		mouse dclick homm3 "scholar".from_left(0)
		wait "Магия Земли"; press Enter; sleep 1s
		mouse dclick homm3 "scholar".from_bottom(0)
		wait "Магия Земли"; press Enter; sleep 1s
		mouse dclick homm3 "scholar"
		wait "Магия Земли"; press Enter; sleep 1s
		mouse dclick homm3 "garden_of_revelation".from_top(0); collect_knowledge()
		mouse dclick homm3 "star_axis".from_top(0); collect_spell()

		mouse dclick img "icons/castle_logo.png"
		mouse click homm3 "hall"
		mouse click "2 этаж гильдии магов"
		mouse click img "icons/purchase.png"
		wait homm3 "mage_guild"
		mouse click homm3 "ok"

		end_day_blue()
	}

	ubuntu {
		wait img "icons/new_day.png"
	}
}

[no_snapshots: true]
test day3: day2 {
	ubuntu {
		mouse click img "icons/map_selection_2.png"

		mouse dclick homm3 "sign".from_top(0);
		wait "Вилабаджо"; mouse click homm3 "ok"

		mouse dclick homm3 "hovel".from_left(0); collect_peasants()
		mouse dclick img "icons/necro_logo.png"
		mouse click homm3 "hall"

		mouse click "Проклятый чертог"
		mouse click img "icons/purchase.png"
		wait homm3 "cursed_temple"; sleep 500ms
		mouse click homm3 "ok"

		end_day_red()
	}

	win7 {
		wait img "icons/new_day.png"
		mouse dclick homm3 "garden_of_revelation".from_top(1); collect_knowledge()
		mouse dclick homm3 "star_axis".from_top(1); collect_spell()
		mouse dclick homm3 "garden_of_revelation".from_top(2); collect_knowledge()

		mouse dclick img "icons/castle_logo.png"
		mouse click homm3 "hall"
		mouse click "3 этаж гильдии магов"
		mouse click img "icons/purchase.png"
		wait homm3 "mage_guild"
		mouse click homm3 "ok"

		end_day_blue()
	}

	ubuntu {
		wait img "icons/new_day.png"
	}
}

[no_snapshots: true]
test day4: day3 {
	ubuntu {
		mouse dclick homm3 "hovel".from_right(5); collect_peasants()
		mouse dclick homm3 "hovel".from_right(4); collect_peasants()
		mouse dclick homm3 "hovel".from_right(3); collect_peasants()
		mouse dclick homm3 "hovel".from_right(2); collect_peasants()
		mouse dclick homm3 "hovel".from_right(1); collect_peasants()
		mouse dclick homm3 "hovel".from_right(0); collect_peasants()

		end_day_red()
	}

	win7 {
		wait img "icons/new_day.png"
		mouse dclick homm3 "star_axis".from_top(2); collect_spell()
		mouse dclick homm3 "garden_of_revelation".from_top(3); collect_knowledge()
		mouse dclick homm3 "star_axis".from_bottom(1); collect_spell()

		end_day_blue()
	}

	ubuntu {
		wait img "icons/new_day.png"
	}
}

[no_snapshots: true]
test day5: day4 {
	ubuntu {
		mouse dclick homm3 "town".center_bottom().move_up(10)
		wait homm3 "hall"
		mouse click img "icons/skelet_machine_built.png"
		wait "Поместите воинов в Машину"
		mouse click homm3 "peasant"
		mouse click img "icons/convert_to_skeletons.png"
		mouse click homm3 "cancel"

		# объединим два отряда вместа
		mouse click homm3 "skeleton".from_right(0);
		mouse click homm3 "skeleton".from_right(1);

		# заходим в меню объединённого отряда
		mouse dclick homm3 "skeleton".from_right(0)

		# нажимаем кнопку апгрейда
		mouse dclick homm3 "skeleton".from_right(0)
		wait "повысить уровень своих войск"
		press Enter
		wait !"повысить уровень своих войск"

		mouse click homm3 "cursed_temple"
		mouse click img "icons/purchase_2.png"

		mouse click homm3 "skeleton".from_top(0)
		mouse click homm3 "skeleton".from_bottom(0)

		mouse click homm3 "ok"

		end_day_red()
	}

	win7 {
		wait img "icons/new_day.png"

		mouse dclick homm3 "garden_of_revelation".from_bottom(0); collect_knowledge()
		mouse dclick homm3 "star_axis".from_bottom(0); collect_spell()

		mouse click img "icons/map_selection_3.png"
		mouse dclick homm3 "magic_spring"
		wait img "icons/ride_off_blue.png"

		mouse click img "icons/end_turn_blue.png" #TODO!
	}

	ubuntu {
		wait img "icons/new_day.png"
	}
}

[no_snapshots: true]
test day6: day5 {
	ubuntu {
		mouse click img "icons/map_selection_4.png".from_top(0)
		mouse dclick img "icons/crossroad_2.png"
		wait img "icons/crossroad_2_with_sandro.png";
		end_day_red()
	}

	win7 {
		wait img "icons/new_day.png"
		mouse click img "icons/ride_blue.png"
		wait "Несколько глотков из волшебного источника"
		press Enter
		mouse dclick homm3 "town".center_bottom().move_up(10)
		mouse click homm3 "mage_guild"
		wait homm3 "spell_destroy_undead"

		mouse click homm3 "ok"
		wait homm3 "hall"
		mouse click homm3 "ok"
		end_day_blue()
	}

	ubuntu {
		wait img "icons/new_day.png"
	}
}

test day7: day6 {
	ubuntu {
		mouse dclick homm3 "garden_of_revelation".from_top(0); collect_knowledge()
		mouse click img "icons/sandro_logo.png"
		hold LeftShift
		mouse click homm3 "skeleton".from_left(0); sleep 100ms
		mouse click homm3 "skeleton".from_left(1); sleep 100ms
		mouse click homm3 "skeleton".from_left(0); sleep 100ms;
		release
		press Enter
		mouse dclick homm3 "hero".from_top(0)
	}

	win7 {
		wait img "icons/wait_active.png"
	}
}

macro blue_battle_routine() {
	wait img "icons/wait_active.png" || "Потери в бою"
	if (check img "icons/wait_active.png") {
		mouse click img "icons/spell_book.png"
		mouse click homm3 "spell_destroy_undead"
		if (check "Это заклинание стоит 15 очков магии" timeout 2s) {
			print "Oh no, we're out of mana!"
			mouse click homm3 "ok"
			mouse click img "icons/close_spellbook.png"
		}

		wait img "icons/wait_active.png" || "Потери в бою"
		if (check img "icons/wait_active.png") {
			mouse click img "icons/defense.png"
		}
	}
}

macro cast_slow_routine() {
	wait img "icons/wait_active.png" || "Потери в бою"
	if (check img "icons/wait_active.png") {
		mouse click img "icons/spell_book.png"
		mouse click homm3 "spell_slow";
		if (check "Это заклинание стоит 5 очков магии" timeout 2s) {
			print "Oh no, we're out of mana!"
			mouse click homm3 "ok"
			mouse click img "icons/close_spellbook.png"
		}
		wait img "icons/wait_active.png"
	}
}

macro check_battle_outcome() {
	if (check "Потери в бою" timeout 2s) {
		if (check "Ваши войска потерпели горькое поражение") {
			abort "We've LOST!"
		} else if (check "Славная победа") {

		}
	}
}

[no_snapshots: true]
test battle: day7 {
	win7 {
		cast_slow_routine()
		mouse click img "icons/fairies_battle_position.png".center_bottom()
	}

	ubuntu {
		wait img "icons/wait_active.png"
		mouse click img "icons/auto_battle.png"
	}

	win7 {
		for (i IN RANGE 5) {
			blue_battle_routine()
		}

		cast_slow_routine()
		mouse click img "icons/fairies_battle_position_2.png"

		for (i IN RANGE 8) {
			blue_battle_routine()
			if (check "Потери в бою" timeout 2s) {
				if (check "Ваши войска потерпели горькое поражение") {
					abort "We've LOST!"
				} break;
			}
		} else {
			abort "The battle lasts longer than we expected"
		}

		print "We've WON!"
		sleep 5s
	}
}