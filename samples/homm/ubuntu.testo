include "../common_ubuntu_desktop.testo"
include "common.testo"

machine ubuntu {
	cpus: 2
	ram: 4Gb
	iso: "${ISO_DIR}/ubuntu-20.04.1-desktop-amd64.iso"
	disk main: {
		size: 20Gb
	}

	nic WAN: {
		attached_to: "internet"
	}

	nic LAN: {
		attached_to: "lan"
		adapter_type: "e1000"
	}
}

test ubuntu_install {
	ubuntu install_ubuntu20_desktop("ubuntu", "ubuntu", "1111")
}

test ubuntu_configure: ubuntu_install {
	#Basic
	ubuntu {
		mouse click "Skip".center_bottom();
		wait "Livepatch"; mouse click "Next".from_right(0).center_bottom()
		wait "Help improve Ubuntu"; mouse click "Next".from_right(0).center_bottom()
		wait "Privacy"; mouse click "Next".from_right(0).center_bottom()
		wait "Ready to go"; mouse click "Done".from_right(0).center_bottom()
		run_app_ubuntu20_desktop("Settings"); mouse click "Power"
		mouse click "5 minutes"
		mouse click "15 minutes"; sleep 500ms
		mouse click "15 minutes"; mouse click "Never"

		mouse click "Power".from_left(0); press Down
		mouse move 0 0; mouse click "Displays"
		mouse click "800 x 600"; press Down*23; mouse click "1024 x 768"
		mouse click "Apply".from_right(0)
		mouse click "Keep Changes"
		wait !"Keep Changes"
		press LeftAlt + F4; wait !"Power"
	}

	#Install guest additions
	ubuntu install_ga_ubuntu20_desktop("ubuntu", "ubuntu", "1111")

	#Network
	ubuntu {
		mouse click img "icons/ubuntu/network.png"
		#
		mouse click img "icons/ubuntu/nic_connecting.png"
		mouse click "Wired Settings"
		wait img "icons/ubuntu/settings_network.png"
		mouse click img "icons/ubuntu/settings.png".from_top(1)
		mouse click "IPv4"
		mouse click "Manual"
		mouse click "Address".from_bottom(0).center_bottom().move_down(10); type "${ubuntu_ip}"
		mouse click "Netmask".from_bottom(0).center_bottom().move_down(10); type "${lan_netmask}"
		mouse click "Apply"
		wait js "find_text().match('Connected').size() == 2"
		press LeftAlt + F4
		wait !"Settings"
	}
}

test ubuntu_install_vcmi: ubuntu_configure {
	ubuntu {
		#Install the vcmi itself
		exec bash """
			sudo apt-add-repository -y ppa:vcmi/ppa
			sudo apt-get update
			sudo apt-get install -y vcmi
		"""

		run_app_ubuntu20_desktop("vcmi launcher")
		wait "VCMI Launcher"; press LeftAlt + F4; wait !"VCMI Launcher"

		#copy HOMM Data
		copyto "${VCMI_DIR}/VCMI_data/Data" "/home/ubuntu/.local/share/vcmi/Data"
		copyto "${VCMI_DIR}/VCMI_data/Mp3" "/home/ubuntu/.local/share/vcmi/Mp3"
		copyto "${VCMI_DIR}/VCMI_data/Mods/vcmi" "/home/ubuntu/.local/share/vcmi/Mods/vcmi"

		copyto "vcmi_settings.json" "/home/ubuntu/.config/vcmi/settings.json"
	}
}
