# Часть 13. JS-селекторы

## С чем Вы познакомитесь

В этом уроке вы познакомитесь с общими концепциями js-селекторами в языке `testo-lang`, а именно:

1. Как с помощью js-селекторов выполнять нетипичные и сложные проверки состояния экрана в действиях `wait` и `check`;
2. Как с помощью js-селекторов перемещать мышку в нужное место, если мощности спецификаторов вам недостаточно.

## Начальные условия

1. Платформа `testo` установлена.
2. Установлен менеджер виртуальных машин `virt-manager`.
3. На хостовой машине имеется прямой (без прокси) доступ в Интернет
4. Имеется установочный образ [Ubuntu Desktop 18.04](https://releases.ubuntu.com/18.04.4/ubuntu-18.04.4-desktop-amd64.iso) с расположением `/opt/iso/ubuntu_desktop.iso`. Местоположение и название установочного файла может быть другим, в этом случае нужно будет соответствующим образом поправить тестовый скрипт.
5. Имеется установочный образ [Ubuntu server 16.04](http://releases.ubuntu.com/16.04/ubuntu-16.04.6-server-amd64.iso) с расположением `/opt/iso/ubuntu_server.iso`. Местоположение и название установочного файла может быть другим, в этом случае нужно будет соответствующим образом поправить тестовый скрипт.
6. (Рекомендовано) Настроена [подсветка синтаксиса](/docs/getting_started/getting_started#настройка-подсветки-языка-testo-lang) `testo-lang` в Sublime Text 3.
7. (Рекомендовано) Проделаны шаги из [десятой части](10_if) и [двенадцатой части](12_mouse)

## Вступление

В предыдущих уроках мы познакомились с действиями `wait`, `check` и `mouse`, и вы могли заметить, что эти действия в большинстве случаев выглядят просто и компактно, но при этом при необходимости могут усложняться. Например, если в `wait` вам требуется проверить одновременное наличие нескольких надписей на экране, то можно воспользоваться оператором `&&`, а если в `mouse` требуется передвинуть курсор мышки вправо от надписи на 50 пикселей, то можно воспользоваться спецификаторами `right_center()` и `move_right(50)`.

Но что делать, если стандартных возможностей действий не хватает для необходимых проверок и действий? Например, как проверить. что на экране именно три экземпляра одной надписи? Как передвинуть курсор относительно не одного, а двух объектов на экране? Примеров достаточно сложных проверок и перемещений может быть очень много, и не всегда стандартных встроенных возможностей языка `testo-lang` может хватать для их имплементации.

Для решения этой проблемы в языке `testo-lang` существуют [JS-селекторы](/docs/js/general). Если вкратце - это небольшие участки кода на языке javascript, которые можно применять внутри действий `wait`, `check` и `mouse`. Внутри этих участков кода (js-сниппетов) вы можете получить информацию об объектах на экране и сделать любую сколько угодно сложную обработку содержимого экрана, и при этом результат этой обработки и будет определять результат работы перечисленных действий. И именно с этим механизмом мы познакомимся в этом уроке.

## С чего начать?

Для начала вернёмся к процессу установки Ubuntu Server, который мы уже неоднократно разбирали (и, казалось бы, уже довели до совершенства). Давайте посмотрим на самый первый экран

![Ubuntu Started](/static/tutorials/1_creating_vm/ubuntu_started.png)

Напомним, что при появлении этого экрана мы должны нажать на Enter, потому что нас устраивает английский язык. Но давайте подумаем, а можем ли мы быть на 100% уверенными, что в качестве языка по-умолчанию будет подсвечен именно English? Что если мы сами явлляемся разработчиками Ubuntu Server и мы бы хотели убедиться, что по умолчанию выделен именно английский язык? Что если в этой сборке у нас всё хорошо, а через 10 коммитов какой-нибудь стажер случайно поломает выбор языка по умолчанию, и мы хотели бы подстраховаться от такого развития событий?

Возникает вопрос - а существует ли способ удостовериться, что пункт `English` именно **выделен**, а не просто существует на экране? На самом деле, такая возможность в языке `testo-lang` есть, но для этого придется воспользоваться JS-селектором.

Давайте подумаем, а как вообще можно формализовать условие "Надпись выделена"? Если задуматься, для этого нам достаточно посмотреть на цвет надписи и на цвет фона этой надписи. Взгляните внимательно на скриншот. Мы видим, что выделенная надпись отличается от всех остальных в первую очередь тем, что цвет текста на этой надписи белый, а фон - серый, чего не наблюдается на других пунктах с другими языками.

И в плафторме Testo существует возможность анализировать цвета самих надписей и фона этих надписей.

```testo
macro install_ubuntu(hostname, login, password = "${default_password}") {
	start
	wait js "find_text().match('English').match_foreground('white').match_background('gray').size() == 1"
	press Enter
	...
```

Итак, что же мы видим? Во-первых, мы воспользовались новой для нас конструкцией - `wait js`, что означает, что мы хотим дождаться не просто строку, а хотим использовать результат работы JS-селектора. В чем разница? `wait "Надпись"` сработает в том случае, когда на экране появится как минимум один экземпляр искомой надписи. `wait js` сработает тогда, когда javascript-код внутри JS-селектора вернёт значение `true`. Если в течение заданного таймаута JS-селектор так и не вернул значение `true`, то действие `wait` завершится с ошибкой.

Сам JS-селектор должен располагаться в кавычках (если сниппет достаточно длинный, то можно разместить его в нескольких строках, воспользовавшись тройными кавычками). Нам нужно составить сниппет таким образом, чтобы он возвращал true в том случае, если на экране, во-первых, просто есть надпись English, а во-вторых, чтобы эта надпись была черной на сером фоне. Для этого мы воспользуемся встроенными функциями, доступными внутри JS-селекторов.

1. Чтобы убедиться, что надпись English вообще присутствет на экране, воспользуемся сначала глобальной встроенной функцией [find_text](/docs/js/global_funcs#find_text()), которая вернет нам объект `Tensor<TextLine>`. В этом объекте будет лежать информация о всех найденных надписях на экране.
2. Далее нам необходимо просто отфильтровать лишнюю информацию из полученного `Tensor<TextLine>`, оставив нужные надписи. В данном случае нас интересуют все надписи `English`, поэтому мы воспользуемся методом [`match`](/docs/js/tensor_textline#match(value)), передав туда в качестве аргумента строку `English`.
3. Теперь мы имеем объект `Tensor<TextLine>`, содержащий только надписи `English` (в данном случае у нас только одна такая надпись). Нам необходимо проанализировать цвет самой надписи и её фона. Для этого по аналогии мы воспользуемся методами [`match_foreground`](/docs/js/tensor_textline#match_foreground(value)) и [`match_background`](/docs/js/tensor_textline#match_background(value)). В качестве цвета шрифта мы передадим `white`, а цвета фона - `gray`.
4. Если после всех фильтраций мы получим `Tensor<TextLine>` с одной строкой (это можно понять, вызывав метод `size()`), то это будет означать, что на экране действительно присутствует роавно одна выделенная надпись `English`, что нас вполне себе устраивает. Такой сниппет вернет true как только необходимая надпись появится на экране - и `wait` отработает успешно, передав управление следующему действию.

Кстати, использование JS-селекторов в запросах вовсе не означает, что в этом же запросе нельзя пользоваться и обычным поиском надписей. Например, мы могли бы немного усилить проверку содержимого первого экрана, добавив поиск надписи "Language"

```testo
macro install_ubuntu(hostname, login, password = "${default_password}") {
	start
	wait "Language" && js "find_text().match('English').match_foreground('white').match_background('gray').size() == 1"
	press Enter
	...
```

Такой `wait` успешно отработает в том случае, если на экране есть надпись `Language` И есть **выделенная** надпись `English`.

Попробуйте запустить тест на установку ОС с изменённым макросом `install_ubuntu` и убедитесь, что он успешно отрабатывает на экране с выбором языка. Попробуйте также поменять в JS-селекторе искомый язык с `English`  на любой другой (например, на Русский) и убедитесь, что теперь `wait` не срабатывает, потому что другие надписи не выделены.

Давайте рассмотрим следующий экран

![How to install](/static/tutorials/2_ubuntu_installation/ubuntu_started_2.png)

В этот раз мы видим, что выделенная надпись отличается от всех остальных только цветом самого текста, а цвет фона не играет никакой роли. Поэтому в этом случае наша проверка будет немного короче:

```testo
macro install_ubuntu(hostname, login, password = "${default_password}") {
	start
	wait js "find_text().match('English').match_foreground('white').match_background('gray').size() == 1"
	press Enter
	wait js "find_text().match('Install Ubuntu Server').match_foreground('white').size() == 1"; press Enter;
	...
```

## Усиливаем проверки при установке Ubuntu Server

Далее мы попадаем на другой экрана выбора языка

![Language](/static/tutorials/13_js/language.png)

Здесь и далее критерием того, что надпись выделена, является белый шрифт на красном фоне. То есть буквально на каждом экране проверки того, что мы нажимаем именно на то, что ожидаем, будет выглядеть так:

```testo
wait "Choose the language" && js "find_text().match('English').match_foreground('white').match_background('red')size() == 1"; press Enter
...
```

Довольно громоздко, не правда ли? И такие проверки будут присутствовать буквально в каждой строчке нашего тестовго сценария.

Но мы можем решить эту проблему, если воспользуемся уже знакомыми нам макросами и сделаем себе небольшое вспомогательное средство:

```testo
macro enter_if_selected(text) {
	if (check js "find_text().match('${text}').match_foreground('white').match_background('red').size() > 0") {
		press Enter
	} else {
		abort "String ${text} is not selected!"
	}
}
```
Этот новый макрос будет проверять, выделена ли нужна надпись, и если выделена - будет нажимать клавишу Enter - то есть, иными словами - выбирать выделенный пункт. Если же нужна надпись не выделена, то будет генерироваться ошибка.

Теперь повсеместное применение таких проверок уже не будет выглядить так громоздко

```testo
wait "Choose the language";	enter_if_selected("English")
...
```

Если модифицировать весь макрос `install_ubuntu`, мы получим такой результат:

```testo
macro install_ubuntu(hostname, login, password = "${default_password}") {
	start
	wait js "find_text().match('English').match_foreground('white').match_background('gray').size() == 1"
	press Enter
	wait js "find_text().match('Install Ubuntu Server').match_foreground('white').size() == 1"; press Enter;
	wait "Choose the language";	enter_if_selected("English")
	wait "Select your location"; enter_if_selected("United States")
	wait "Detect keyboard layout?";	enter_if_selected("No")
	wait "Country of origin for the keyboard"; enter_if_selected("English (US)")
	wait "Keyboard layout"; enter_if_selected("English (US)")
	wait "No network interfaces detected" || "Primary network interface" || "Hostname:" timeout 5m
	if (check "No network interfaces detected") {
		enter_if_selected("Continue")
	} else if (check "Primary network interface"){
		enter_if_selected("ens3")
	}
	wait "Hostname:" timeout 5m; press Backspace*36; type "${hostname}"; press Tab; enter_if_selected("Continue")
	wait "Full name for the new user"; type "${login}"; press Tab; enter_if_selected("Continue")
	wait "Username for your account"; press Tab; enter_if_selected("Continue")
	wait "Choose a password for the new user"; type "${password}"; press Tab*2; enter_if_selected("Continue")
	wait "Re-enter password to verify"; type "${password}"; press Tab*2; enter_if_selected("Continue")
	if (check "Use weak password" timeout 3s) {
		press Left; enter_if_selected("Yes")
	}
	wait "Encrypt your home directory?"; enter_if_selected("No")
	wait "Select your time zone" || "Is this time zone correct?" timeout 2m;
	if (check "Select your time zone") {
		enter_if_selected("Eastern")
	} else {
		enter_if_selected("Yes")
	}
	wait "Partitioning method"; enter_if_selected("Guided - use entire disk and set up LVM")
	wait "Select disk to partition"; enter_if_selected("SCSI1")
	wait "Write the changes to disks and configure LVM?"; press Left; enter_if_selected("Yes")
	wait "Amount of volume group to use for guided partitioning"; press Tab; enter_if_selected("Continue")
	wait "Write the changes to disks?"; press Left; enter_if_selected("Yes")
	wait "HTTP proxy information" timeout 3m; press Tab; enter_if_selected("Continue")
	wait "How do you want to manage upgrades" timeout 6m; enter_if_selected("No automatic updates")
	wait "Choose software to install"; press Tab; enter_if_selected("Continue")
	wait "Install the GRUB boot loader to the master boot record?" timeout 10m; enter_if_selected("Yes")
	wait "Installation complete" timeout 1m; 

	unplug dvd; enter_if_selected("Continue")
	wait "${hostname} login:" timeout 2m; type "${login}"; press Enter
	wait "Password:"; type "${password}"; press Enter
	wait "Welcome to Ubuntu"
}
```

Как видите, наш макрос не стал выглядеть более громоздко, зато мы получили два важных новых преимущества:

1. Теперь на каждом шагу мы проверяем, что мы кликаем именно на ту надпись, на которую ожидаем кликнуть - что ещё больше усиливает проверочные свойства тестов с участием этого макроса;
2. Теперь достаточно взглянуть на код макроса чтобы понять, какие именно действия мы предпринимаем на каждом шагу. Вместо довольно безликих `press Enter`, которые могут означать всё, что угодно, мы получаем осознанное понятное действие, благодаря которому такой макрос читать и сопровождать намного проще.

Таким образом благодаря одному новому макросу `enter_if_selected` и применению JS-селектора мы смогли ещё больше улучшить и без того хороший и отлаженный макрос.

## JS-селекторы в перемещениях курсора

Мы с вами разобрали применение JS-селекторов в дейтвии `wait` и условии `check`. Но помимо этого JS-селекторы можно применять и в действии `mouse` для указания координат перемещения курсора мышки, если по какой-то причине вам не хватает стандартного функционала спецификаторов. Для того, чтобы это продемостнировать, давайте представим, что мы хотим протестировать работу с ячейками в LibreOffice Calc. Для этого давайте возьмём наработки из [двенадцатой части](12_mouse) наших уроков, где мы автоматизировали установку Ubuntu Desktop 18.04.

Перенесём все свои наработки из двенадцатого урока в наши текущие файлы:

1. Объявление виртуальной машины `ubuntu_desktop` перенесём в `declarations.testo`;
2. Параметры `login` и `hostname` переименуем в `desktop_login` и `desktop_hostname`;
3. Тест `install_ubuntu` переименуем в `install_ubuntu_desktop` и перенесем в файл `tests.testo`;
4. В тесте `install_ubuntu_desktop` уберем действие `mouse click "Minimal installation"` чтобы был установлен LibreOffice;
5. Тест `mouse_demo` не переносим, он нам не понадобится.

После этого запустите тест `install_ubuntu_desktop` и убедитесь, что он успешно проходит.

```testo
test install_ubuntu_desktop {
	ubuntu_desktop {
		start
		wait "English"
		press Enter
		wait "Try Ubuntu without installing"; press Down, Enter
		wait "Welcome" timeout 5m
		mouse click "Continue".center_bottom()
		wait "Keyboard layout"
		mouse click "Continue".center_bottom()
		wait "Updates and other software"
		mouse click "Download updates while installing"; mouse click "Continue"
		wait "Installation type"; mouse move 0 0; mouse click "Install Now".center_bottom()
		wait "Write the changes to disks?"; mouse click "Continue".from_bottom(0).center_bottom()
		wait "Where are you?"; mouse click "Continue".center_bottom()
		wait "Who are you?";

		mouse click "Your name".right_center().move_right(20); type "${desktop_login}"
		mouse click "Your computer's name".right_center().move_right(20); press LeftCtrl + A, Delete;  type "${desktop_hostname}"
		mouse click "password:".from_top(0).right_center().move_right(20); type "${default_password}"
		mouse click "password:".from_top(1).right_center().move_right(20); type "${default_password}"
		mouse click "Continue".center_bottom()
		mouse click "Restart Now" timeout 15m
		wait "Please remove the installation medium" timeout 2m;
		unplug dvd; stop; start
		wait "${desktop_login}" && "Not listed?" timeout 3m

		mouse click "${desktop_login}";
		wait "Password"; type "${default_password}"; mouse click "Sign In"
		wait "Welcome to Ubuntu"
	}
}
```

<Terminal height="600px">
	<span className="">user$ sudo testo run mouse.testo --stop_on_fail --param ISO_DIR /opt/iso --assume_yes --test_spec install_ubuntu_desktop<br/></span>
	<span className="blue bold">TESTS TO RUN:<br/></span>
	<span className="magenta ">install_ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Preparing the environment for test </span>
	<span className="yellow ">install_ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Restoring snapshot </span>
	<span className="yellow ">initial</span>
	<span className="blue "> for virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Running test </span>
	<span className="yellow ">install_ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Starting virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">English </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Pressing key </span>
	<span className="yellow ">ENTER </span>
	<span className="blue ">on virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Try Ubuntu without installing </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Pressing key </span>
	<span className="yellow ">DOWN </span>
	<span className="blue ">on virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Pressing key </span>
	<span className="yellow ">ENTER </span>
	<span className="blue ">on virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Welcome </span>
	<span className="blue ">for 5m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Continue.center_bottom() </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Keyboard layout </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Continue.center_bottom() </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Updates and other software </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Download updates while installing </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Continue </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Installation type </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse moving </span>
	<span className="blue ">on coordinates </span>
	<span className="yellow ">0 0 </span>
	<span className="blue ">in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Install Now.center_bottom() </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Write the changes to disks? </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Continue.from_bottom(0).center_bottom() </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Where are you? </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Continue.center_bottom() </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Who are you? </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Your name.right_center().move_right(20) </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Typing </span>
	<span className="yellow ">"desktop" </span>
	<span className="blue ">with interval 30ms in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Your computer's name.right_center().move_right(20) </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Pressing key </span>
	<span className="yellow ">LEFTCTRL+A </span>
	<span className="blue ">on virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Pressing key </span>
	<span className="yellow ">DELETE </span>
	<span className="blue ">on virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Typing </span>
	<span className="yellow ">"desktop-PC" </span>
	<span className="blue ">with interval 30ms in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">password:.from_top(0).right_center().move_right(20) </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Typing </span>
	<span className="yellow ">"ThisIsStrongPassword" </span>
	<span className="blue ">with interval 30ms in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">password:.from_top(1).right_center().move_right(20) </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Typing </span>
	<span className="yellow ">"ThisIsStrongPassword" </span>
	<span className="blue ">with interval 30ms in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Continue.center_bottom() </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Restart Now </span>
	<span className="blue ">with timeout 15m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Please remove the installation medium </span>
	<span className="blue ">for 2m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Unplugging dvd </span>
	<span className="yellow "> </span>
	<span className="blue ">from virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Stopping virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Starting virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">desktop && Not listed? </span>
	<span className="blue ">for 3m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">desktop </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Password </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Typing </span>
	<span className="yellow ">"ThisIsStrongPassword" </span>
	<span className="blue ">with interval 30ms in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Mouse clicking </span>
	<span className="blue ">on </span>
	<span className="yellow ">Sign In </span>
	<span className="blue ">with timeout 1m in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Waiting </span>
	<span className="yellow ">Welcome to Ubuntu </span>
	<span className="blue ">for 1m with interval 1s in virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="blue ">[  0%] Taking snapshot </span>
	<span className="yellow ">install_ubuntu_desktop</span>
	<span className="blue "> for virtual machine </span>
	<span className="yellow ">ubuntu_desktop<br/></span>
	<span className="green bold">[100%] Test </span>
	<span className="yellow bold">install_ubuntu_desktop</span>
	<span className="green bold"> PASSED in 0h:8m:1s<br/></span>
	<span className="blue bold">PROCESSED TOTAL 1 TESTS IN 0h:8m:1s<br/></span>
	<span className="blue bold">UP-TO-DATE: 0<br/></span>
	<span className="green bold">RUN SUCCESSFULLY: 1<br/></span>
	<span className="red bold">FAILED: 0<br/></span>
	<span className="">user$ </span>
</Terminal>

Давайте сделаем новый тест, в котором мы будем запускать LibreOffice Calc

```testo
test libre_office_launch: install_ubuntu_desktop {
	ubuntu_desktop {
		mouse click "Welcome to Ubuntu"
		mouse click "Quit"
		wait !"Welcome to Ubuntu"
		mouse click "Activities"
		wait "Type to search"; type "libre"
		mouse click "LibreOffice Calc"
		wait "Untitled 1 - LibreOffice Calc"
	}
}
```

Теперь мы окажемся на следующем экране:

![LibreOffice started](/static/tutorials/13_js/libreoffice_started.png)

После чего добавим еще один новый тест `libre_office_demo`

```testo
test libre_office_demo: libre_office_launch {
	ubuntu_desktop {
		abort "stop here"
	}
}
```

Ну а теперь представим себе такую задачу. Что если нам в тесте требуется кликнуть на какую-то конкретную ячейку в LibreOffice Calc? С помощью спецификаторов эту задачу нельзя решить тривиальным способом. Мы могли бы попытаться вычислить количество пикселей, которое нам нужно отступить от какой-то надписи на экране, чтобы попасть в нужную ячейку, но это слишком трудоемко, неинформативно, сложно поддерживать, да еще и придется каждый раз подгонять количество пикселей заново.

Допустим, нам необходимо кликнуть **правой** кнопкой мыши на ячейке `G15`.

Для того, чтобы переместить курсор на эту ячейку, мы воспользуемся JS-селектором:

```testo
test libre_office_demo: libre_office_launch {
	ubuntu_desktop {
		mouse rclick js """
			row_index = find_text().match('15').from_left(0).center()
			column_index = find_text().match('G').from_right(0).center()

			result = {x: column_index.x, y: row_index.y}
		"""

		wait "G15"
	}
}
```

Давайте рассмотрим этот кусочек кода поподробнее.

Для применения JS-селектора при перемещнии курсора необходимо сразу после типа действия с мышью (в нашем случае это правый клик) указать ключевое слово `js`, а затем написать сам javascript-сниппет. В отличие от `wait` и `check`, где количество селекторов (и обычных надписей) в выражениях для поиска неограничено, здесь мы можем использовать **только один** JS-селектор. Если вы решили использовать JS-селектор в действии `mouse`, то вы не можете в этом же действии использовать обычный формат перемещения курсора на основе надписей и спецификаторов.

Как же работает JS-селектор в действии `mouse`? Если в действии `wait` JS-селектор должен возвращать `true` или `false`, то в случае с `mouse` JS-селектор должен возвращать **объект с полями `x` и `y`**. Этот объект символизирует точку, куда следует переместь курсор. Объект должен содержать абсолютное значение координаты Х и координаты Y.

Основываясь на этом, мы построили свой JS-селектор по такому алгоритму:

1. Сначала мы найдем координаты центра надписи "15" - это номер той строчки, на которой расположена интересующая нас ячейка. Для этого мы сначала найдем все надписи "15" по уже знакомой схеме, а затем возьмем из найденных надписей самую левую с помощью метода [`from_left()`](/docs/js/tensor_textline#from_left(index)) класса `Tensor<TextLine>`. Т.к. нас интересует на надпись "15", а координаты её центра, то мы вызовем метод [`center()`](/docs/js/tensor_textline#center()), который возвращает объект класса [`Point`](/docs/js/point), содержащий координаты.
2. Теперь мы повторим процесс для нахождения координат центра надписи "G".
3. В качестве результата работы мы вернем новый объект `result`, в котором нам необходимо прописать два поля: `x` и `y`. Координату Х мы возьмем из "G", а координату Y - из "15". Таким образом мы найдём и вернем пересечение строки "15" и столбца "G".

Давайте запустим наш тест и убедимся, что мы добились нужного результата

![G15](/static/tutorials/13_js/G15.png)

Как мы видим, все отработало так, как мы хотели.

## Итоги

Хотя чаще всего стандартных средств языка `testo-lang` хватает чтобы организовать практически любые требующиеся проверки состояния экрана или перемещения курсора мышки, иногда в особо сложных проверках и перемещениях вам могут пригодиться JS-селекторы. Внутри этих селекторов вы можете обращаться к содержимому экрана и писть сколько угодно сложную логику обработки.

Часто такие селекторы могут несколько испортить внешний вид ваших тестов, но вы всегда можете инкапсулировать логику этих селекторов в макросы и сделать свои тесты снова удобочитаемыми.

Готовые скрипты можно найти [здесь](https://github.com/CIDJEY/Testo_tutorials/tree/master/13)